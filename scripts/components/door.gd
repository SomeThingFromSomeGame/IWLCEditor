extends GameObject
class_name Door
const SCENE:PackedScene = preload("res://scenes/objects/door.tscn")

enum TYPE {SIMPLE, COMBO, GATE}

const FRAME:Texture2D = preload("res://assets/game/door/frame.png")
const FRAME_NEGATIVE:Texture2D = preload("res://assets/game/door/frameNegative.png")
const FRAME_HIGH:Texture2D = preload("res://assets/game/door/frameHigh.png")
const FRAME_MAIN:Texture2D = preload("res://assets/game/door/frameMain.png")
const FRAME_DARK:Texture2D = preload("res://assets/game/door/frameDark.png")

const SPEND_HIGH:Texture2D = preload("res://assets/game/door/spendHigh.png")
const SPEND_MAIN:Texture2D = preload("res://assets/game/door/spendMain.png")
const SPEND_DARK:Texture2D = preload("res://assets/game/door/spendDark.png")
const GATE_FILL:Texture2D = preload("res://assets/game/door/gateFill.png")

const CRUMBLED_1X1:Texture2D = preload("res://assets/game/door/aura/crumbled1x1.png")
const CRUMBLED_1X2:Texture2D = preload("res://assets/game/door/aura/crumbled1x2.png")
const CRUMBLED_2X2:Texture2D = preload("res://assets/game/door/aura/crumbled2x2.png")
const CRUMBLED_MATERIAL:ShaderMaterial = preload("res://resources/materials/crumbledDrawMaterial.tres")

const PAINTED_1X1:Texture2D = preload("res://assets/game/door/aura/painted1x1.png")
const PAINTED_1X2:Texture2D = preload("res://assets/game/door/aura/painted1x2.png")
const PAINTED_2X2:Texture2D = preload("res://assets/game/door/aura/painted2x2.png")
const PAINTED_BASE:Texture2D = preload("res://assets/game/door/aura/paintedBase.png")
const PAINTED_MATERIAL:ShaderMaterial = preload("res://resources/materials/paintedDrawMaterial.tres")

const FROZEN_1X1:Texture2D = preload("res://assets/game/door/aura/frozen1x1.png")
const FROZEN_1X2:Texture2D = preload("res://assets/game/door/aura/frozen1x2.png")
const FROZEN_2X2:Texture2D = preload("res://assets/game/door/aura/frozen2x2.png")
const FROZEN_3X2:Texture2D = preload("res://assets/game/door/aura/frozen3x2.png")
const FROZEN_MATERIAL:ShaderMaterial = preload("res://resources/materials/frozenDrawMaterial.tres")

const GLITCH_HIGH:Texture2D = preload("res://assets/game/door/glitch/high.png")
const GLITCH_MAIN:Texture2D = preload("res://assets/game/door/glitch/main.png")
const GLITCH_DARK:Texture2D = preload("res://assets/game/door/glitch/dark.png")

static var GLITCH:ColorsTextureLoader = ColorsTextureLoader.new("res://assets/game/door/glitch/$c.png",Game.TEXTURED_COLORS, false, false, {capitalised=false})

const TEXTURE_RECT:Rect2 = Rect2(Vector2.ZERO,Vector2(64,64)) # size of all the door textures
const CORNER_SIZE:Vector2 = Vector2(9,9) # size of door ninepatch corners
const GLITCH_CORNER_SIZE:Vector2 = Vector2(16,16) # except glitchdraw is a different size
const TILE:RenderingServer.NinePatchAxisMode = RenderingServer.NinePatchAxisMode.NINE_PATCH_TILE # just to save characters
const STRETCH:RenderingServer.NinePatchAxisMode = RenderingServer.NinePatchAxisMode.NINE_PATCH_STRETCH # just to save characters

const CREATE_PARAMETERS:Array[StringName] = [
	&"position"
]
const PROPERTIES:Array[StringName] = [
	&"id", &"position", &"size",
	&"colorSpend", &"copies", &"infCopies", &"type",
	&"frozen", &"crumbled", &"painted"
]
static var ARRAYS:Dictionary[StringName,Variant] = {
	&"remoteLocks":RemoteLock
}

var colorSpend:Game.COLOR = Game.COLOR.WHITE
var copies:PackedInt64Array = M.ONE
var infCopies:PackedInt64Array = M.ZERO # axes with infinite copies
var type:TYPE = TYPE.SIMPLE
var frozen:bool = false
var crumbled:bool = false
var painted:bool = false

var drawDropShadow:RID
var drawScaled:RID # also draws aura breaker fills
var drawAuraBreaker:RID
var drawGlitch:RID
var drawMain:RID
var drawCrumbled:RID
var drawPainted:RID
var drawFrozen:RID
var drawCopies:RID
var drawNegative:RID

var locks:Array[Lock] = []
var remoteLocks:Array[RemoteLock] = []

@onready var locksParent:Node2D = %locksParent

const COPIES_COLOR = Color("#edeae7")
const COPIES_OUTLINE_COLOR = Color("#3e2d1c")

func _init() -> void: size = Vector2(32,32)

func _ready() -> void:
	drawDropShadow = RenderingServer.canvas_item_create()
	drawScaled = RenderingServer.canvas_item_create()
	drawAuraBreaker = RenderingServer.canvas_item_create()
	drawGlitch = RenderingServer.canvas_item_create()
	drawMain = RenderingServer.canvas_item_create()
	drawCrumbled = RenderingServer.canvas_item_create()
	drawPainted = RenderingServer.canvas_item_create()
	drawFrozen = RenderingServer.canvas_item_create()
	drawCopies = RenderingServer.canvas_item_create()
	drawNegative = RenderingServer.canvas_item_create()
	RenderingServer.canvas_item_set_material(drawGlitch,Game.GLITCH_MATERIAL.get_rid())
	RenderingServer.canvas_item_set_material(drawNegative,Game.NEGATIVE_MATERIAL.get_rid())
	RenderingServer.canvas_item_set_z_index(drawDropShadow,-3)
	RenderingServer.canvas_item_set_z_index(drawCopies,2)
	RenderingServer.canvas_item_set_z_index(drawNegative,2)
	RenderingServer.canvas_item_set_parent(drawDropShadow,get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawScaled,get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawAuraBreaker,get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawGlitch,get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawMain,get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawCrumbled, %auraParent.get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawPainted, %auraParent.get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawFrozen, %auraParent.get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawCopies,get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawNegative,get_canvas_item())
	Game.connect(&"goldIndexChanged",queue_redraw)

func _freed() -> void:
	RenderingServer.free_rid(drawDropShadow)
	RenderingServer.free_rid(drawScaled)
	RenderingServer.free_rid(drawAuraBreaker)
	RenderingServer.free_rid(drawGlitch)
	RenderingServer.free_rid(drawMain)
	RenderingServer.free_rid(drawCrumbled)
	RenderingServer.free_rid(drawPainted)
	RenderingServer.free_rid(drawFrozen)
	RenderingServer.free_rid(drawCopies)
	RenderingServer.free_rid(drawNegative)

func _draw() -> void:
	RenderingServer.canvas_item_clear(drawDropShadow)
	RenderingServer.canvas_item_clear(drawScaled)
	RenderingServer.canvas_item_clear(drawAuraBreaker)
	RenderingServer.canvas_item_clear(drawGlitch)
	RenderingServer.canvas_item_clear(drawMain)
	RenderingServer.canvas_item_clear(drawCrumbled)
	RenderingServer.canvas_item_clear(drawPainted)
	RenderingServer.canvas_item_clear(drawFrozen)
	RenderingServer.canvas_item_clear(drawCopies)
	RenderingServer.canvas_item_clear(drawNegative)
	if !active and Game.playState == Game.PLAY_STATE.PLAY: return
	if type != TYPE.GATE: RenderingServer.canvas_item_add_rect(drawDropShadow,Rect2(Vector2(3,3),size),Game.DROP_SHADOW_COLOR)
	drawDoor(drawScaled,drawAuraBreaker,drawGlitch,drawMain,
		size,colorAfterCurse(),colorAfterGlitch(),type,
		gateAlpha,
		len(locks) > 0 and (locks[0].isNegative() if type == TYPE.SIMPLE else M.negative(M.sign(ipow()))),
		(Game.playState == Game.PLAY_STATE.PLAY and drawComplex) or (Game.playState == Game.PLAY_STATE.EDIT and M.nex(copies)),
		animState != ANIM_STATE.RELOCK or animPart > 2
	)
	var rect:Rect2 = Rect2(Vector2.ZERO, size)
	# auras
	drawAuras(drawCrumbled,drawPainted,drawFrozen,
		frozen if Game.playState == Game.PLAY_STATE.EDIT else gameFrozen,
		crumbled if Game.playState == Game.PLAY_STATE.EDIT else gameCrumbled,
		painted if Game.playState == Game.PLAY_STATE.EDIT else gamePainted,
		rect)
	# anim overlays
	if animState == ANIM_STATE.ADD_COPY: RenderingServer.canvas_item_add_rect(drawNegative,rect,Color(Color.WHITE,animAlpha))
	elif animState == ANIM_STATE.RELOCK: RenderingServer.canvas_item_add_rect(drawCopies,rect,Color(Color.WHITE,animAlpha)) # just to be on top of everything else
	# copies
	if Game.playState == Game.PLAY_STATE.PLAY:
		if M.neq(gameCopies, M.ONE) or M.ex(infCopies): TextDraw.outlinedCentered(Game.FKEYX,drawCopies,"×"+M.strWithInf(gameCopies,infCopies),COPIES_COLOR,COPIES_OUTLINE_COLOR,20,Vector2(size.x/2,-8))
	else:
		if M.neq(copies, M.ONE) or M.ex(infCopies): TextDraw.outlinedCentered(Game.FKEYX,drawCopies,"×"+M.strWithInf(copies,infCopies),COPIES_COLOR,COPIES_OUTLINE_COLOR,20,Vector2(size.x/2,-8))

static func drawDoor(doorDrawScaled:RID,doorDrawAuraBreaker:RID,doorDrawGlitch:RID,doorDrawMain:RID,
	doorSize:Vector2,
	doorBaseColor:Game.COLOR, doorGlitchColor:Game.COLOR,
	doorType:TYPE,
	doorGateAlpha:float,
	negative:bool=false, doorDrawComplex:bool=false, drawFill:bool=true
) -> void:
	var rect:Rect2 = Rect2(Vector2.ZERO, doorSize)
	# fill
	if doorType == TYPE.GATE:
		RenderingServer.canvas_item_add_texture_rect(doorDrawMain,rect,GATE_FILL,true,Color(Color.WHITE,lerp(0.35,1.0,doorGateAlpha)))
		#outline
		RenderingServer.canvas_item_add_rect(doorDrawMain,Rect2(Vector2(0,-1),Vector2(doorSize.x,1)),Color.BLACK.blend(Color(Color.WHITE,doorGateAlpha)))
		RenderingServer.canvas_item_add_rect(doorDrawMain,Rect2(Vector2(0,doorSize.y),Vector2(doorSize.x,1)),Color.BLACK.blend(Color(Color.WHITE,doorGateAlpha)))
		RenderingServer.canvas_item_add_rect(doorDrawMain,Rect2(Vector2(-1,0),Vector2(1,doorSize.y)),Color.BLACK.blend(Color(Color.WHITE,doorGateAlpha)))
		RenderingServer.canvas_item_add_rect(doorDrawMain,Rect2(Vector2(doorSize.x,0),Vector2(1,doorSize.y)),Color.BLACK.blend(Color(Color.WHITE,doorGateAlpha)))
	else:
		if drawFill:
			if doorBaseColor in Game.TEXTURED_COLORS:
				var tileTexture:bool = doorBaseColor in Game.TILED_TEXTURED_COLORS
				RenderingServer.canvas_item_add_texture_rect(doorDrawScaled,rect,Game.COLOR_TEXTURES.current([doorBaseColor]),tileTexture)
			elif doorBaseColor == Game.COLOR.GLITCH:
				RenderingServer.canvas_item_add_nine_patch(doorDrawGlitch,rect,TEXTURE_RECT,SPEND_HIGH,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,Game.highTone[Game.COLOR.GLITCH])
				RenderingServer.canvas_item_add_nine_patch(doorDrawGlitch,rect,TEXTURE_RECT,SPEND_MAIN,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,Game.mainTone[Game.COLOR.GLITCH])
				RenderingServer.canvas_item_add_nine_patch(doorDrawGlitch,rect,TEXTURE_RECT,SPEND_DARK,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,Game.darkTone[Game.COLOR.GLITCH])
				if doorGlitchColor != Game.COLOR.GLITCH:
					if doorGlitchColor in Game.TEXTURED_COLORS:
						RenderingServer.canvas_item_add_nine_patch(doorDrawMain,rect,TEXTURE_RECT,GLITCH.current([doorGlitchColor]),GLITCH_CORNER_SIZE,GLITCH_CORNER_SIZE,TILE,TILE)
					else:
						RenderingServer.canvas_item_add_nine_patch(doorDrawMain,rect,TEXTURE_RECT,GLITCH_HIGH,GLITCH_CORNER_SIZE,GLITCH_CORNER_SIZE,TILE,TILE,true,Game.highTone[doorGlitchColor])
						RenderingServer.canvas_item_add_nine_patch(doorDrawMain,rect,TEXTURE_RECT,GLITCH_MAIN,GLITCH_CORNER_SIZE,GLITCH_CORNER_SIZE,TILE,TILE,true,Game.mainTone[doorGlitchColor])
						RenderingServer.canvas_item_add_nine_patch(doorDrawMain,rect,TEXTURE_RECT,GLITCH_DARK,GLITCH_CORNER_SIZE,GLITCH_CORNER_SIZE,TILE,TILE,true,Game.darkTone[doorGlitchColor])
			elif doorBaseColor in [Game.COLOR.ICE, Game.COLOR.MUD, Game.COLOR.GRAFFITI]:
				RenderingServer.canvas_item_set_material(doorDrawScaled,Game.NO_MATERIAL.get_rid())
				RenderingServer.canvas_item_add_nine_patch(doorDrawScaled,rect,TEXTURE_RECT,SPEND_HIGH,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,Game.highTone[doorBaseColor])
				RenderingServer.canvas_item_add_nine_patch(doorDrawScaled,rect,TEXTURE_RECT,SPEND_MAIN,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,Game.mainTone[doorBaseColor])
				RenderingServer.canvas_item_add_nine_patch(doorDrawScaled,rect,TEXTURE_RECT,SPEND_DARK,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,Game.darkTone[doorBaseColor])
				drawAuras(doorDrawAuraBreaker,doorDrawAuraBreaker,doorDrawAuraBreaker,doorBaseColor==Game.COLOR.ICE,doorBaseColor==Game.COLOR.MUD,doorBaseColor==Game.COLOR.GRAFFITI,rect)
			else:
				RenderingServer.canvas_item_add_nine_patch(doorDrawMain,rect,TEXTURE_RECT,SPEND_HIGH,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,Game.highTone[doorBaseColor])
				RenderingServer.canvas_item_add_nine_patch(doorDrawMain,rect,TEXTURE_RECT,SPEND_MAIN,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,Game.mainTone[doorBaseColor])
				RenderingServer.canvas_item_add_nine_patch(doorDrawMain,rect,TEXTURE_RECT,SPEND_DARK,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,Game.darkTone[doorBaseColor])
		# frame
		if doorDrawComplex:
			RenderingServer.canvas_item_add_nine_patch(doorDrawMain,rect,TEXTURE_RECT,FRAME_HIGH,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,Color.from_hsv(Game.complexViewHue,0.4901960784,1))
			RenderingServer.canvas_item_add_nine_patch(doorDrawMain,rect,TEXTURE_RECT,FRAME_MAIN,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,Color.from_hsv(Game.complexViewHue,0.7058823529,0.9019607843))
			RenderingServer.canvas_item_add_nine_patch(doorDrawMain,rect,TEXTURE_RECT,FRAME_DARK,CORNER_SIZE,CORNER_SIZE,TILE,TILE,true,Color.from_hsv(Game.complexViewHue,1,0.7450980392))
		elif negative: RenderingServer.canvas_item_add_nine_patch(doorDrawMain,rect,TEXTURE_RECT,FRAME_NEGATIVE,CORNER_SIZE,CORNER_SIZE)
		else: RenderingServer.canvas_item_add_nine_patch(doorDrawMain,rect,TEXTURE_RECT,FRAME,CORNER_SIZE,CORNER_SIZE)

static func drawAuras(objectDrawCrumbled:RID,objectDrawPainted:RID,objectDrawFrozen:RID,objectFrozen:bool,objectCrumbled:bool,objectPainted:bool,rect:Rect2) -> void:
	if objectCrumbled:
		RenderingServer.canvas_item_set_material(objectDrawCrumbled,CRUMBLED_MATERIAL.get_rid())
		RenderingServer.canvas_item_set_instance_shader_parameter(objectDrawCrumbled, &"size", Vector2(0,0))
		if rect.size == Vector2(32,32): RenderingServer.canvas_item_add_texture_rect(objectDrawCrumbled,rect,CRUMBLED_1X1)
		elif rect.size == Vector2(32,64): RenderingServer.canvas_item_add_texture_rect(objectDrawCrumbled,rect,CRUMBLED_1X2)
		elif rect.size == Vector2(64,64): RenderingServer.canvas_item_add_texture_rect(objectDrawCrumbled,rect,CRUMBLED_2X2)
		else:
			RenderingServer.canvas_item_set_instance_shader_parameter(objectDrawCrumbled, &"size", rect.size)
			RenderingServer.canvas_item_add_rect(objectDrawCrumbled,rect,Color.WHITE)
	if objectPainted:
		RenderingServer.canvas_item_set_material(objectDrawPainted,PAINTED_MATERIAL.get_rid())
		RenderingServer.canvas_item_set_instance_shader_parameter(objectDrawPainted, &"scale", Vector2(0,0))
		if rect.size == Vector2(32,32): RenderingServer.canvas_item_add_texture_rect(objectDrawPainted,rect,PAINTED_1X1)
		elif rect.size == Vector2(32,64): RenderingServer.canvas_item_add_texture_rect(objectDrawPainted,rect,PAINTED_1X2)
		elif rect.size == Vector2(64,64): RenderingServer.canvas_item_add_texture_rect(objectDrawPainted,rect,PAINTED_2X2)
		else:
			RenderingServer.canvas_item_set_instance_shader_parameter(objectDrawPainted, &"scale", rect.size/128)
			RenderingServer.canvas_item_add_texture_rect(objectDrawPainted,rect,PAINTED_BASE,true)
	if objectFrozen:
		RenderingServer.canvas_item_set_material(objectDrawFrozen,FROZEN_MATERIAL.get_rid())
		RenderingServer.canvas_item_set_instance_shader_parameter(objectDrawFrozen, &"size", Vector2(0,0))
		if rect.size == Vector2(32,32): RenderingServer.canvas_item_add_texture_rect(objectDrawFrozen,rect,FROZEN_1X1)
		elif rect.size == Vector2(32,64): RenderingServer.canvas_item_add_texture_rect(objectDrawFrozen,rect,FROZEN_1X2)
		elif rect.size == Vector2(64,64): RenderingServer.canvas_item_add_texture_rect(objectDrawFrozen,rect,FROZEN_2X2)
		elif rect.size == Vector2(96,64): RenderingServer.canvas_item_add_texture_rect(objectDrawFrozen,rect,FROZEN_3X2)
		else:
			RenderingServer.canvas_item_set_instance_shader_parameter(objectDrawFrozen, &"size", rect.size)
			RenderingServer.canvas_item_add_rect(objectDrawFrozen,rect,Color.WHITE)

func receiveMouseInput(event:InputEventMouse) -> bool:
	# resizing
	if !editor.edgeResizing or editor.componentDragged: return false
	var dragCornerSize:Vector2 = Vector2(8,8)/editor.cameraZoom
	var diffSign:Vector2 = Editor.rectSign(Rect2(position+dragCornerSize,size-dragCornerSize*2), editor.mouseWorldPosition)
	if !diffSign: return false
	elif !diffSign.x: editor.mouse_default_cursor_shape = Control.CURSOR_VSIZE
	elif !diffSign.y: editor.mouse_default_cursor_shape = Control.CURSOR_HSIZE
	elif (diffSign.x > 0) == (diffSign.y > 0): editor.mouse_default_cursor_shape = Control.CURSOR_FDIAGSIZE
	else: editor.mouse_default_cursor_shape = Control.CURSOR_BDIAGSIZE
	if Editor.isLeftClick(event):
		editor.startSizeDrag(self, diffSign)
		return true
	return false

func propertyChangedInit(property:StringName) -> void:
	if property == &"type":
		match type:
			TYPE.SIMPLE:
				if len(locks) == 0: addLock()
				elif len(locks) > 1:
					for lockIndex in range(1,len(locks)):
						removeLock(lockIndex)
				locks[0]._simpleDoorUpdate()
			TYPE.COMBO:
				if !Mods.active(&"NstdLockSize"):
					for lock in locks: lock._coerceSize()
			TYPE.GATE:
				if !Mods.active(&"NstdLockSize"):
					for lock in locks: lock._coerceSize()
				Changes.addChange(Changes.PropertyChange.new(self,&"colorSpend",Game.COLOR.WHITE))
				Changes.addChange(Changes.PropertyChange.new(self,&"copies",M.ONE))
				Changes.addChange(Changes.PropertyChange.new(self,&"frozen",false))
				Changes.addChange(Changes.PropertyChange.new(self,&"crumbled",false))
				Changes.addChange(Changes.PropertyChange.new(self,&"painted",false))
	if property == &"size" and type == TYPE.SIMPLE and len(locks) > 0: locks[0]._simpleDoorUpdate() # ghhghghhh TODO: figure this out
	if property in [&"copies", &"infCopies"] and M.neq(M.across(M.acrabs(copies), infCopies), infCopies):
		if M.ex(M.r(infCopies)) and M.nex(M.r(copies)): Changes.addChange(Changes.PropertyChange.new(self,&"copies", M.Ncn(M.saxis(M.r(copies)), M.ir(copies))))
		if M.ex(M.i(infCopies)) and M.nex(M.i(copies)): Changes.addChange(Changes.PropertyChange.new(self,&"copies",M.Ncn(M.r(copies), M.saxis(M.ir(copies)))))

func propertyChangedDo(property:StringName) -> void:
	super(property)
	if editor and property == &"type" and editor.findProblems:
		for lock in locks: editor.findProblems.findProblems(lock)
	if property == &"type":
		z_index = 7 if type == TYPE.GATE else 0
	if property in [&"size", &"type"]:
		%shape.shape.size = size
		%shape.position = size/2
		%interactShape.shape.size = size
		%interactShape.position = size/2
		if type == TYPE.COMBO: %interactShape.shape.size += Vector2(2,2)
		elif type == TYPE.SIMPLE: %shape.shape.size -= Vector2(2,2)
	if property in [&"size", &"position"]:
		for remoteLock in remoteLocks: remoteLock.queue_redraw()

func addLock() -> void:
	Changes.addChange(Changes.CreateComponentChange.new(Lock,{&"position":getFirstFreePosition(),&"parentId":id}))
	if len(locks) == 1 and type != Door.TYPE.GATE: Changes.addChange(Changes.PropertyChange.new(self,&"type",TYPE.SIMPLE))
	elif type == Door.TYPE.SIMPLE: Changes.addChange(Changes.PropertyChange.new(self,&"type",TYPE.COMBO))
	Changes.bufferSave()

func duplicateLock(lock:Lock) -> void:
	var newLock:Lock = Changes.addChange(Changes.CreateComponentChange.new(Lock,{&"position":getFirstFreePosition(lock.getOffset(), lock.size),&"parentId":id})).result
	Changes.addChange(Changes.PropertyChange.new(self,&"type",TYPE.COMBO))
	for property in Lock.PROPERTIES:
		if property not in Lock.CREATE_PARAMETERS and property != &"id":
			Changes.addChange(Changes.PropertyChange.new(newLock,property,Changes.copy(lock.get(property))))
	Changes.bufferSave()

func getFirstFreePosition(lockOffset:Vector2=Vector2(7,7), lockSize:Vector2=Vector2(18,18)) -> Vector2:
	for y in floor(size.y/32):
		for x in floor(size.x/32):
			var rect:Rect2 = Rect2(Vector2(32*x,32*y)-lockOffset, lockSize)
			var overlaps:bool = false
			for lock in locks:
				if Rect2(lock.position-lock.getOffset(), lock.size).intersects(rect):
					overlaps = true
					break
			if overlaps: continue
			return Vector2(32*x,32*y)
	return Vector2.ZERO

func removeLock(index:int) -> void:
	Changes.addChange(Changes.DeleteComponentChange.new(locks[index]))
	if type == Door.TYPE.SIMPLE: Changes.addChange(Changes.PropertyChange.new(self,&"type",TYPE.COMBO))
	Changes.bufferSave()

func deletedInit() -> void:
	for remoteLock in remoteLocks:
		Changes.addChange(Changes.ComponentArrayPopAtChange.new(remoteLock,&"doors",remoteLock.doors.find(self)))

func reindexLocks() -> void:
	var iter:int = 0
	var nonArmamentIter:int = 0
	var armamentIter:int = 0
	for lock in locks:
		lock.index = iter
		if lock.armament:
			lock.displayIndex = armamentIter
			if lock.get_parent() != %armamentLocksParent:
				lock.get_parent().remove_child(lock)
				%armamentLocksParent.add_child(lock)
				%armamentLocksParent.move_child(lock,armamentIter)
			armamentIter += 1
		else:
			lock.displayIndex = nonArmamentIter
			if lock.get_parent() != %locksParent:
				lock.get_parent().remove_child(lock)
				%locksParent.add_child(lock)
				%locksParent.move_child(lock,armamentIter)
			nonArmamentIter += 1
		iter += 1

# ==== PLAY ==== #
var gameCopies:PackedInt64Array = M.ONE
var gameFrozen:bool = false
var gameCrumbled:bool = false
var gamePainted:bool = false
var cursed:bool = false
var curseColor:Game.COLOR
var glitchMimic:Game.COLOR = Game.COLOR.GLITCH
var curseGlitchMimic:Game.COLOR = Game.COLOR.GLITCH

enum ANIM_STATE {IDLE, ADD_COPY, RELOCK}
var animState:ANIM_STATE = ANIM_STATE.IDLE
var animTimer:float = 0
var animAlpha:float = 0
var addCopySound:AudioStreamPlayer
var animPart:int = 0
var gateAlpha:float = 1
var gateOpen:bool = false
var gateBufferCheck:bool = false
var curseTimer:float = 0
var drawComplex:bool = false

var justOpened:bool = false # for player jumped off door check

func _process(delta:float) -> void:
	if cursed and active:
		curseTimer += delta
		if curseTimer >= 2:
			curseTimer -= 2
			makeCurseParticles(curseColor,1,0.2,0.3)
	match animState:
		ANIM_STATE.IDLE: animTimer = 0; animAlpha = 0
		ANIM_STATE.ADD_COPY:
			animTimer += delta*60
			if addCopySound: addCopySound.pitch_scale = 1 + 0.015*animTimer
			var animLength:float = lerp(50,10,Game.fastAnimSpeed)
			animAlpha = 1 - animTimer/animLength
			if animTimer >= animLength: animState = ANIM_STATE.IDLE
			queue_redraw()
		ANIM_STATE.RELOCK:
			animTimer += delta*60
			var animLength:float = lerp(60,12,Game.fastAnimSpeed)
			match animPart:
				0: if animTimer >= lerp(25,5,Game.fastAnimSpeed):
					AudioManager.play(preload("res://resources/sounds/door/relock.wav"))
					animPart += 1
				1: if animTimer >= lerp(40,8,Game.fastAnimSpeed):
					AudioManager.play(preload("res://resources/sounds/door/masterNegative.wav"))
					animAlpha = 1
					animPart += 1
				2: if animTimer >= lerp(50,10,Game.fastAnimSpeed):
					animPart += 1
					for lock in locks: lock.queue_redraw()
				3:
					animAlpha -= delta*6 # 0.1 per frame, 60fps
			if animTimer >= animLength:
				animState = ANIM_STATE.IDLE
			queue_redraw()
	if type == TYPE.GATE:
		if gateBufferCheck and !overlappingPlayer() and !Game.player.overlapping(%interact):
			GameChanges.addChange(GameChanges.PropertyChange.new(self,&"gateBufferCheck",false))
			GameChanges.addChange(GameChanges.PropertyChange.new(self,&"gateOpen",false))
			GameChanges.bufferSave()
		if !gateOpen and gateAlpha < 1:
			gateAlpha = min(gateAlpha+delta*6, 1)
			queue_redraw()
		elif gateOpen and gateAlpha > 0:
			gateAlpha = max(gateAlpha-delta*6, 0)
			queue_redraw()
	if drawComplex or (Game.playState == Game.PLAY_STATE.EDIT and M.nex(copies)): queue_redraw()

func start() -> void:
	gameCopies = copies
	gameFrozen = frozen
	gameCrumbled = crumbled
	gamePainted = painted
	animState = ANIM_STATE.IDLE
	animTimer = 0
	animAlpha = 0
	animPart = 0
	complexCheck()
	if type == TYPE.GATE:
		if overlappingPlayer():
			gateOpen = true
			gateBufferCheck = true
		else: gateCheck(Game.player, true)
	propertyGameChangedDo(&"gateOpen")
	super()

# avoids 1 frame delay
func overlappingPlayer() -> bool: return Rect2(Game.player.position - Vector2(6,12), Vector2(12,21)).intersects(Rect2(position, size))

func stop() -> void:
	cursed = false
	curseTimer = 0
	gateAlpha = 1
	gateOpen = false
	gateBufferCheck = false
	drawComplex = false
	glitchMimic = Game.COLOR.GLITCH
	curseGlitchMimic = Game.COLOR.GLITCH
	justOpened = false
	super()

func tryOpen(player:Player) -> void:
	if type == TYPE.GATE: return
	if animState != ANIM_STATE.IDLE: return
	if gameFrozen or gameCrumbled or gamePainted:
		var gateArmamentImmunities:Array[Game.COLOR] = player.getArmamentImmunities()
		if hasColor(Game.COLOR.PURE): return
		if int(gameFrozen) + int(gameCrumbled) + int(gamePainted) > 1: return
		if gameFrozen and (M.nex(player.key[Game.COLOR.ICE]) or Game.COLOR.ICE in gateArmamentImmunities): return
		if gameCrumbled and (M.nex(player.key[Game.COLOR.MUD]) or Game.COLOR.MUD in gateArmamentImmunities): return
		if gamePainted and (M.nex(player.key[Game.COLOR.GRAFFITI]) or Game.COLOR.GRAFFITI in gateArmamentImmunities): return
	else:
		if player.explodey and tryDynamiteOpen(player): return
		if player.masterCycle == 1 and tryMasterOpen(player): return
		if player.masterCycle == 2 and tryQuicksilverOpen(player): return

	if M.ex(gameCopies): # although nothing (yet) can make a door 0 copy without destroying it
		var willCrash:bool = false
		var wontOpen:bool = false
		for lock in locks:
			if !lock.canOpen(player):
				if lock.colorAfterAurabreaker() == Game.COLOR.NONE: willCrash = true
				else: return
			elif lock.colorAfterAurabreaker() == Game.COLOR.NONE: wontOpen = true
		for lock in remoteLocks:
			if !lock.satisfied: return
		if willCrash: Game.crash(); return
		if wontOpen: return
	var cost:PackedInt64Array = M.ZERO
	var glistenCost:PackedInt64Array = M.ZERO
	for lock in locks:
		if lock.type == lock.TYPE.GLISTENING:
			glistenCost = M.add(cost, lock.getCost(player))
		else:
			cost = M.add(cost, lock.getCost(player))
	for lock in remoteLocks:
		cost = M.add(cost, lock.cost)
	
	var spendColor:Game.COLOR = colorAfterAurabreaker()
	player.changeKeys(spendColor, M.sub(player.key[spendColor],cost))
	player.changeGlisten(spendColor, M.sub(player.glisten[spendColor], glistenCost))
	
	GameChanges.addChange(GameChanges.PropertyChange.new(self, &"gameCopies", M.sub(gameCopies, M.across(ipow(), M.sub(M.allAxes(), infCopies)))))
	
	if gameFrozen or gameCrumbled or gamePainted: AudioManager.play(preload("res://resources/sounds/door/deaura.wav"))
	else:
		match type:
			TYPE.SIMPLE:
				if locks[0].type == Lock.TYPE.BLAST: AudioManager.play(preload("res://resources/sounds/door/blast.wav"))
				elif colorAfterCurse() == Game.COLOR.MASTER and locks[0].colorAfterCurse() == Game.COLOR.MASTER: AudioManager.play(preload("res://resources/sounds/door/master.wav"))
				else: AudioManager.play(preload("res://resources/sounds/door/simple.wav"))
			TYPE.COMBO: AudioManager.play(preload("res://resources/sounds/door/combo.wav"))
		Game.setGlitch(colorAfterAurabreaker())

	if M.nex(gameCopies): destroy()
	else: relockAnimation()
	GameChanges.bufferSave()

func tryMasterOpen(player:Player) -> bool:
	if hasColor(Game.COLOR.MASTER): return false
	if hasColor(Game.COLOR.PURE): return false

	var openedForwards:bool = M.positive(M.sign(M.across(gameCopies, player.masterMode)))
	GameChanges.addChange(GameChanges.PropertyChange.new(self, &"gameCopies", M.sub(gameCopies, M.across(player.masterMode, M.sub(M.allAxes(), infCopies)))))
	player.changeKeys(Game.COLOR.MASTER, M.sub(player.key[Game.COLOR.MASTER], player.masterMode))
	
	if openedForwards:
		AudioManager.play(preload("res://resources/sounds/door/master.wav"))
		if M.nex(gameCopies): destroy()
		else: relockAnimation()
	else:
		AudioManager.play(preload("res://resources/sounds/door/masterNegative.wav"))
		addCopyAnimation()

	player.dropMaster()
	GameChanges.bufferSave()
	return true

func tryQuicksilverOpen(player:Player) -> bool:
	if hasColor(Game.COLOR.QUICKSILVER): return false
	if hasColor(Game.COLOR.PURE): return false

	var cost:PackedInt64Array = M.ZERO
	var glistenCost:PackedInt64Array = M.ZERO
	for lock in locks:
		if lock.type == lock.TYPE.GLISTENING:
			glistenCost = M.add(cost, lock.getCost(player, player.masterMode))
		else:
			cost = M.add(cost, lock.getCost(player, player.masterMode))
	for lock in remoteLocks:
		cost = M.add(cost, lock.cost)
	player.changeKeys(Game.COLOR.QUICKSILVER, M.sub(player.key[Game.COLOR.QUICKSILVER], player.masterMode))
	var spendColor:Game.COLOR = colorAfterAurabreaker()
	player.changeKeys(spendColor, M.sub(player.key[spendColor],cost))
	player.changeGlisten(spendColor, M.sub(player.glisten[spendColor], glistenCost))

	AudioManager.play(preload("res://resources/sounds/door/master.wav"))
	relockAnimation()

	Game.setGlitch(colorAfterGlitch())

	player.dropMaster()
	GameChanges.bufferSave()

	return true

func tryDynamiteOpen(player:Player) -> bool:
	if hasColor(Game.COLOR.DYNAMITE): return false
	if hasColor(Game.COLOR.PURE): return false

	var openedForwards:bool
	var openedBackwards:bool

	if M.simplies(gameCopies, player.key[Game.COLOR.DYNAMITE]) and M.nonNegative(M.sub(M.along(player.key[Game.COLOR.DYNAMITE], gameCopies), M.acrabs(gameCopies))) and M.nex(infCopies):
		# if the door can open, open it
		player.changeKeys(Game.COLOR.DYNAMITE, M.sub(player.key[Game.COLOR.DYNAMITE], gameCopies))
		GameChanges.addChange(GameChanges.PropertyChange.new(self, &"gameCopies", M.ZERO))
		
		openedForwards = true
	else:
		openedForwards = M.hasPositive(M.along(player.key[Game.COLOR.DYNAMITE], gameCopies))
		openedBackwards = M.hasNonPositive(M.along(player.key[Game.COLOR.DYNAMITE], gameCopies))

		GameChanges.addChange(GameChanges.PropertyChange.new(self, &"gameCopies", M.sub(gameCopies, M.across(player.key[Game.COLOR.DYNAMITE], M.sub(M.allAxes(),infCopies)))))
		player.changeKeys(Game.COLOR.DYNAMITE, M.ZERO)

	if openedForwards:
		AudioManager.play(preload("res://resources/sounds/door/explode.wav"))
		if M.nex(gameCopies): destroy()
		else: relockAnimation()
		add_child(ExplosionParticle.new(size/2,1))
	if openedBackwards:
		AudioManager.play(preload("res://resources/sounds/door/explodeNegative.wav"))
		if !openedForwards:
			addCopyAnimation()
			add_child(ExplosionParticle.new(size/2,-1))

	GameChanges.bufferSave()
	return true

func hasColor(color:Game.COLOR) -> bool:
	if colorAfterGlitch() == color: return true
	for lock in locks: if lock.colorAfterGlitch() == color: return true
	return false

func hasBaseColor(color:Game.COLOR) -> bool:
	if colorSpend == color: return true
	for lock in locks: if lock.color == color: return true
	return false

func destroy() -> void:
	GameChanges.addChange(GameChanges.PropertyChange.new(self, &"active", false))
	var color:Game.COLOR = colorAfterCurse()
	if type == TYPE.SIMPLE: color = locks[0].colorAfterCurse()
	makeDebris(Debris, color)
	justOpened = true

func addCopyAnimation() -> void:
	animState = ANIM_STATE.ADD_COPY
	animTimer = 0
	animAlpha = 0
	animPart = 0
	Game.fasterAnims()
	addCopySound = AudioManager.play(preload("res://resources/sounds/door/addCopy.wav"))
	var color:Game.COLOR = colorAfterCurse()
	if type == TYPE.SIMPLE: color = locks[0].colorAfterCurse()
	makeDebris(AddCopyDebris, color)

func relockAnimation() -> void:
	animState = ANIM_STATE.RELOCK
	animTimer = 0
	animAlpha = 0
	animPart = 0
	Game.fasterAnims()
	for lock in locks: lock.queue_redraw()
	var color:Game.COLOR = colorAfterCurse()
	if type == TYPE.SIMPLE: color = locks[0].colorAfterCurse()
	makeDebris(RelockDebris, color)

func makeDebris(debrisType:GDScript, debrisColor:Game.COLOR) -> void:
	for y in floor(size.y/16):
		for x in floor(size.x/16):
			add_child(debrisType.new(debrisColor,Vector2(x*16,y*16)))

func propertyGameChangedDo(property:StringName) -> void:
	if property == &"active":
		%collision.process_mode = PROCESS_MODE_INHERIT if active else PROCESS_MODE_DISABLED
		%interact.process_mode = PROCESS_MODE_INHERIT if active else PROCESS_MODE_DISABLED
		for remoteLock in remoteLocks: remoteLock.checkDoors()
	if property == &"gateOpen" and type == TYPE.GATE:
		%collision.process_mode = PROCESS_MODE_DISABLED if gateOpen else PROCESS_MODE_INHERIT
	if property == &"gameCopies": complexCheck()

func gateCheck(player:Player, starting:bool=false) -> void:
	var shouldOpen:bool = true
	var willCrash:bool = false
	for lock in locks:
		if !lock.canOpen(player):
			if lock.colorAfterGlitch() == Game.COLOR.NONE: willCrash = true
			else: shouldOpen = false
		elif lock.colorAfterGlitch() == Game.COLOR.NONE: shouldOpen = false
	for lock in remoteLocks:
		if !lock.satisfied: shouldOpen = false
	if shouldOpen and willCrash: Game.crash(); return
	if gateOpen and !shouldOpen:
		if player.overlapping(%interact): GameChanges.addChange(GameChanges.PropertyChange.new(self,&"gateBufferCheck",true))
		else: GameChanges.addChange(GameChanges.PropertyChange.new(self,&"gateOpen",false))
	elif !gateOpen and shouldOpen:
		GameChanges.addChange(GameChanges.PropertyChange.new(self,&"gateBufferCheck",false))
		if starting: gateOpen = true
		else: GameChanges.addChange(GameChanges.PropertyChange.new(self,&"gateOpen",true))

func auraCheck(player:Player) -> void:
	if type == TYPE.GATE: return
	if animState != ANIM_STATE.IDLE: return
	var deAuraed:bool = false
	if player.auraRed and gameFrozen and !hasColor(Game.COLOR.MAROON):
		GameChanges.addChange(GameChanges.PropertyChange.new(self,&"gameFrozen",false))
		makeDebris(Debris, Game.COLOR.WHITE)
		deAuraed = true
	if player.auraGreen and gameCrumbled and !hasColor(Game.COLOR.FOREST):
		GameChanges.addChange(GameChanges.PropertyChange.new(self,&"gameCrumbled",false))
		makeDebris(Debris, Game.COLOR.BROWN)
		deAuraed = true
	if player.auraBlue and gamePainted and !hasColor(Game.COLOR.NAVY):
		GameChanges.addChange(GameChanges.PropertyChange.new(self,&"gamePainted",false))
		makeDebris(Debris, Game.COLOR.ORANGE)
		deAuraed = true
	var auraed:bool = false
	if player.auraMaroon and !gameFrozen and !hasColor(Game.COLOR.RED) and !isAllColorAfterCurse(Game.COLOR.ICE):
		GameChanges.addChange(GameChanges.PropertyChange.new(self,&"gameFrozen",true))
		makeDebris(Debris, Game.COLOR.WHITE)
		auraed = true
	if player.auraForest and !gameCrumbled and !hasColor(Game.COLOR.GREEN) and !isAllColorAfterCurse(Game.COLOR.MUD):
		GameChanges.addChange(GameChanges.PropertyChange.new(self,&"gameCrumbled",true))
		makeDebris(Debris, Game.COLOR.BROWN)
		auraed = true
	if player.auraNavy and !gamePainted and !hasColor(Game.COLOR.BLUE) and !isAllColorAfterCurse(Game.COLOR.GRAFFITI):
		GameChanges.addChange(GameChanges.PropertyChange.new(self,&"gamePainted",true))
		makeDebris(Debris, Game.COLOR.ORANGE)
		auraed = true
	if deAuraed or auraed:
		AudioManager.play(preload("res://resources/sounds/door/deaura.wav"))
		GameChanges.bufferSave()

func isAllColor(color:Game.COLOR) -> bool:
	if colorSpend != color: return false
	for lock in locks: if lock.color != color: return false
	return true

func isAllColorAfterCurse(color:Game.COLOR) -> bool:
	if colorAfterCurse() != color: return false
	for lock in locks: if lock.colorAfterCurse() != color: return false
	return true

func curseCheck(player:Player) -> void:
	if type == TYPE.GATE: return
	if animState != ANIM_STATE.IDLE: return
	if hasColor(Game.COLOR.PURE): return
	var willCurse:bool = player.curseMode > 0 and (!cursed or (curseColor != player.curseColor and curseColor != Game.COLOR.PURE))
	var willCurseRedundant:bool = willCurse and isAllColor(player.curseColor)
	if willCurse and !willCurseRedundant:
		GameChanges.addChange(GameChanges.PropertyChange.new(self,&"cursed",true))
		GameChanges.addChange(GameChanges.PropertyChange.new(self,&"curseColor",player.curseColor))
		makeCurseParticles(curseColor, 1, 0.2, 0.5)
		AudioManager.play(preload("res://resources/sounds/door/curse.wav"))
		GameChanges.bufferSave()
	elif cursed and (willCurseRedundant or (player.curseMode < 0 and curseColor == player.curseColor)):
		GameChanges.addChange(GameChanges.PropertyChange.new(self,&"cursed",false))
		if curseColor == Game.COLOR.GLITCH:
			GameChanges.addChange(GameChanges.PropertyChange.new(self,&"curseGlitchMimic",Game.COLOR.GLITCH))
		if willCurseRedundant:
			makeCurseParticles(player.curseColor, 1, 0.2, 0.5)
			AudioManager.play(preload("res://resources/sounds/door/curse.wav"))
		else:
			makeCurseParticles(Game.COLOR.BROWN, -1, 0.2, 0.5)
			AudioManager.play(preload("res://resources/sounds/door/decurse.wav"))
		GameChanges.bufferSave()

func makeCurseParticles(color:Game.COLOR, mode:int, scaleMin:float=1,scaleMax:float=1) -> void:
	for y in floor(size.y/16):
		for x in floor(size.x/16):
			add_child(CurseParticle.Temporary.new(color, mode, Vector2(x,y)*16+Vector2.ONE*randf_range(4,12), randf_range(scaleMin,scaleMax)))

func colorAfterCurse() -> Game.COLOR:
	if cursed and curseColor != Game.COLOR.PURE: return curseColor
	return colorSpend

func colorAfterGlitch() -> Game.COLOR:
	var base:Game.COLOR = colorAfterCurse()
	if base == Game.COLOR.GLITCH: return curseGlitchMimic if cursed and curseColor != Game.COLOR.PURE else glitchMimic
	return base

func colorAfterAurabreaker() -> Game.COLOR:
	if gameFrozen: return Game.COLOR.ICE
	if gameCrumbled: return Game.COLOR.MUD
	if gamePainted: return Game.COLOR.GRAFFITI
	return colorAfterGlitch()

func ipow() -> PackedInt64Array: # for complex view
	if Game.playState == Game.PLAY_STATE.EDIT: return M.ONE
	# if extant, return current
	if M.ex(M.across(gameCopies, Game.player.complexMode)): return M.saxis(M.across(gameCopies, Game.player.complexMode))
	# return the other axis
	return M.saxis(M.across(gameCopies, M.axibs(M.rotate(Game.player.complexMode))))

func complexCheck() -> void:
	drawComplex = Game.playState != Game.PLAY_STATE.EDIT and M.nex(M.across(ipow(), Game.player.complexMode))
	queue_redraw()

func setGlitch(setColor:Game.COLOR) -> void:
	if !cursed or curseColor == Game.COLOR.PURE: GameChanges.addChange(GameChanges.PropertyChange.new(self, &"glitchMimic", setColor))
	elif curseColor == Game.COLOR.GLITCH: GameChanges.addChange(GameChanges.PropertyChange.new(self, &"curseGlitchMimic", setColor))
	for lock in locks:
		if !cursed or curseColor == Game.COLOR.PURE or lock.armament: GameChanges.addChange(GameChanges.PropertyChange.new(lock, &"glitchMimic", setColor))
		lock.queue_redraw()
	queue_redraw()
	if type == TYPE.GATE:
		gateCheck(Game.player)
		Game.player.bufferCheckKeys() # if armaments

func armamentColors() -> Array[Game.COLOR]:
	var colors:Array[Game.COLOR]
	for lock in locks:
		if lock.armament and lock.colorAfterGlitch() not in colors: colors.append(lock.colorAfterGlitch())
	return colors

class Debris extends Node2D:
	const FRAME:Texture2D = preload("res://assets/game/door/debris/frame.png")
	const HIGH:Texture2D = preload("res://assets/game/door/debris/high.png")
	const MAIN:Texture2D = preload("res://assets/game/door/debris/main.png")
	const DARK:Texture2D = preload("res://assets/game/door/debris/dark.png")

	var color:Game.COLOR
	var opacity:float = 1
	var velocity:Vector2 = Vector2.ZERO
	var acceleration:Vector2 = Vector2.ZERO
	var fadeSpeed:float

	const FPS:float = 60

	func _init(_color:Game.COLOR,_position) -> void:
		color = _color
		position = _position
	
	func _ready() -> void:
		velocity.x = randf_range(-1.2,1.2)
		velocity.y = randf_range(-4,-3)
		acceleration.y = randf_range(0.4,0.5)
		fadeSpeed = 0.04
	
	func _physics_process(_delta:float) -> void:
		opacity -= fadeSpeed
		modulate.a = opacity
		if opacity <= 0: queue_free()

		position += velocity
		velocity += acceleration

	func _draw() -> void:
		var rect:Rect2 = Rect2(Vector2.ZERO,Vector2(16,16))
		RenderingServer.canvas_item_add_texture_rect(get_canvas_item(),rect,FRAME)
		RenderingServer.canvas_item_add_texture_rect(get_canvas_item(),rect,HIGH,false,Game.highTone[color])
		RenderingServer.canvas_item_add_texture_rect(get_canvas_item(),rect,MAIN,false,Game.mainTone[color])
		RenderingServer.canvas_item_add_texture_rect(get_canvas_item(),rect,DARK,false,Game.darkTone[color])

class AddCopyDebris extends Debris:
	
	func _ready() -> void:
		velocity = Vector2(0.8,0).rotated(randf_range(0,TAU))
		fadeSpeed = 0.03

	func _draw() -> void:
		var rect:Rect2 = Rect2(Vector2.ZERO,Vector2(16,16))
		RenderingServer.canvas_item_add_texture_rect(get_canvas_item(),rect,FRAME)
		RenderingServer.canvas_item_add_texture_rect(get_canvas_item(),rect,HIGH,false,Game.highTone[color].inverted())
		RenderingServer.canvas_item_add_texture_rect(get_canvas_item(),rect,MAIN,false,Game.mainTone[color].inverted())
		RenderingServer.canvas_item_add_texture_rect(get_canvas_item(),rect,DARK,false,Game.darkTone[color].inverted())

class RelockDebris extends Debris:
	var angle:float = randf_range(0,TAU)
	var speed:float = 1.5
	var startPosition:Vector2
	var part:int = 0 # part of the anim
	var timer:int = 0
	var whiteAmt:float = 0

	func _ready() -> void:
		startPosition = position

	func _physics_process(_delta:float) -> void:
		match part:
			0:
				speed = max(speed - 0.06, 0.3)
				velocity = Vector2(speed,0).rotated(angle)
				position += Vector2(speed,0).rotated(angle)
				if timer >= lerp(25,5, Game.fastAnimSpeed): part += 1; timer = 0
			1:
				position += (startPosition - position) * 0.3
				if position.distance_squared_to(startPosition) < 1: position = startPosition
				whiteAmt = min(whiteAmt+0.0666666667, 1)
				queue_redraw()
				if timer >= lerp(26,5, Game.fastAnimSpeed): queue_free()
		timer += 1

	func _draw() -> void:
		var rect:Rect2 = Rect2(Vector2.ZERO,Vector2(16,16))
		RenderingServer.canvas_item_add_texture_rect(get_canvas_item(),rect,FRAME)
		RenderingServer.canvas_item_add_texture_rect(get_canvas_item(),rect,HIGH,false,Game.highTone[color])
		RenderingServer.canvas_item_add_texture_rect(get_canvas_item(),rect,MAIN,false,Game.mainTone[color])
		RenderingServer.canvas_item_add_texture_rect(get_canvas_item(),rect,DARK,false,Game.darkTone[color])
		RenderingServer.canvas_item_add_rect(get_canvas_item(),rect,Color(Color.WHITE,whiteAmt))
