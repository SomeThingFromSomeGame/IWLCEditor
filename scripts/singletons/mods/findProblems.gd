extends PanelContainer
class_name FindProblems

@onready var editor:Editor = get_node("/root/editor")
@onready var modsWindow:ModsWindow = get_parent()
@onready var problemsLabel:Label = %problemsLabel
var buttonGroup:ButtonGroup = ButtonGroup.new()
var firstButton:bool = false

var problems:int = 0:
	set(value):
		problems = value
		%saveChanges.disabled = problems > 0
		if problems == 0: %saveChanges.text = "Save Changes"
		elif problems == 1: %saveChanges.text = "1 problem"
		else: %saveChanges.text = str(problems) + " problems"

var problemDisplays:Dictionary[StringName,Dictionary] = {} # Dictionary[mod, Dictionary[type, problemdisplay]]
var shownDisplay:ProblemDisplay
var isReady:bool = false

func _ready() -> void:
	buttonGroup.pressed.connect(_modSelected)

func setup() -> void:
	isReady = false
	editor.findProblems = self
	firstButton = true
	problems = 0
	for child in %modsAdded.get_children(): child.queue_free()
	for child in %modsRemoved.get_children(): child.queue_free()
	for mod in Mods.mods.values(): mod.clearProblems()
	
	problemDisplays = {}
	for mod in Mods.mods.keys():
		problemDisplays[mod] = {}
		for problemType in Mods.mods[mod].problems.keys():
			problemDisplays[mod][problemType] = preload("res://scenes/problemDisplay.tscn").instantiate().setup(mod,problemType,self)
	
	for object in Game.objects.values():
		object.problems.clear()
		findProblems(object)
	for component in Game.components.values():
		component.problems.clear()
		findProblems(component)
	for mod in Mods.mods.keys():
		if mod in modsWindow.modsAdded: %modsAdded.add_child(ModSelectButton.new(self,mod))
		elif mod in modsWindow.modsRemoved: %modsRemoved.add_child(ModSelectButton.new(self,mod))
	isReady = true

func _modSelected(button:ModSelectButton) -> void:
	%modName.text = button.mod.name
	var anyProblems:bool = false
	for child in %problems.get_children(): %problems.remove_child(child)
	if shownDisplay: shownDisplay.stopShowing()

	for problemType in button.mod.problems.keys():
		%problems.add_child(problemDisplays[button.modId][problemType])
		problemDisplays[button.modId][problemType].setTexts()
		if len(button.mod.problems[problemType]) != 0:
			anyProblems = true
	%problemsLabel.text = "Problems found:" if anyProblems else "No problems here"

func findProblems(component:GameComponent) -> void:
	if component is Lock:
		if &"DisconnectedLock" in modsWindow.modsRemoved:
			var rect:Rect2 = Rect2(component.position, component.size)
			var bounds:Rect2 = Rect2(component.getOffset(), component.parent.size)
			noteProblem(&"DisconnectedLock", &"DisconnectedLock", component, !bounds.intersects(rect))
	else:
		if &"OutOfBounds" in modsWindow.modsRemoved:
			var rect:Rect2 = Rect2(component.position, component.size)
			noteProblem(&"OutOfBounds", &"OutOfBounds", component, !Game.levelBounds.intersects(rect))

	match component.get_script():
		KeyBulk:
			findColorProblems(component, component.color)
			if &"C5" in modsWindow.modsRemoved:
				noteProblem(&"C5", &"CurseKeyType", component, component.type == KeyBulk.TYPE.CURSE)
			if &"PartialInfKey" in modsWindow.modsRemoved:
				noteProblem(&"PartialInfKey", &"PartialInfKey", component, component.infinite not in [0, 1])
			if &"Glistening" in modsWindow.modsRemoved:
				noteProblem(&"Glistening", &"GlisteningKey", component, component.glistening)
		Lock, RemoteLock:
			findColorProblems(component, component.color)
			if component is Lock:
				if &"NstdLockSize" in modsWindow.modsRemoved:
					noteProblem(&"NstdLockSize", &"NstdLockSize", component, component.parent.type != Door.TYPE.SIMPLE and component.size not in Lock.SIZES)
				if &"MoreLockConfigs" in modsWindow.modsRemoved:
					noteProblem(&"MoreLockConfigs", &"NstdLockConfig", component, component.parent.type != Door.TYPE.SIMPLE and component.configuration in [
						Lock.CONFIGURATION.spr7A, Lock.CONFIGURATION.spr9A, Lock.CONFIGURATION.spr9B, Lock.CONFIGURATION.spr10A, Lock.CONFIGURATION.spr11A, Lock.CONFIGURATION.spr13A,
						Lock.CONFIGURATION.spr24B
					])
			if &"ZeroCostLock" in modsWindow.modsRemoved:
				noteProblem(&"ZeroCostLock", &"ZeroCostLock", component, M.nex(component.count))
			if &"C1" in modsWindow.modsRemoved:
				noteProblem(&"C1", &"RemoteLock", component, component is RemoteLock)
				noteProblem(&"C1", &"LockNegated", component, component.negated)
			if &"C3" in modsWindow.modsRemoved:
				noteProblem(&"C3", &"ExactLock", component, component.type == Lock.TYPE.EXACT)
				noteProblem(&"C3", &"PartialBlastLock", component, component.type == Lock.TYPE.BLAST and (component.isPartial or M.neq(component.count, component.denominator)))
				noteProblem(&"C3", &"PartialBlastLock", component, component.type == Lock.TYPE.ALL and (component.isPartial or M.neq(component.count, M.ONE) or M.neq(component.denominator, M.ONE)))
			if &"C5" in modsWindow.modsRemoved:
				noteProblem(&"C5", &"LockArmament", component, component.armament)
			if &"Glistening" in modsWindow.modsRemoved:
				noteProblem(&"Glistening", &"GlisteningLock", component, component.type == Lock.TYPE.GLISTENING)
		Door:
			findColorProblems(component, component.colorSpend)
			if &"ZeroCopies" in modsWindow.modsRemoved:
				noteProblem(&"ZeroCopies", &"ZeroCopies", component, M.nex(component.copies))
			if &"InfCopies" in modsWindow.modsRemoved:
				noteProblem(&"InfCopies", &"InfCopies", component, M.ex(component.infCopies))
		KeyCounter:
			if &"MoreKeyCounterWidths" in modsWindow.modsRemoved:
				noteProblem(&"MoreKeyCounterWidths", &"NstdKeyCounterWidth", component, KeyCounter.WIDTH_AMOUNT.find(component.size.x) in [KeyCounter.WIDTH.VLONG, KeyCounter.WIDTH.EXLONG])
		KeyCounterElement:
			findColorProblems(component, component.color)

func findColorProblems(component:GameComponent, color:Game.COLOR) -> void:
	if &"NoneColor" in modsWindow.modsRemoved:
		noteProblem(&"NoneColor", &"NoneColor", component, color == Game.COLOR.NONE)
	if &"C2" in modsWindow.modsRemoved:
		noteProblem(&"C2", &"DynamiteColor", component, color == Game.COLOR.DYNAMITE)
		noteProblem(&"C2", &"QuicksilverColor", component, color == Game.COLOR.QUICKSILVER)
	if &"C4" in modsWindow.modsRemoved:
		noteProblem(&"C4", &"DarkAuraColor", component, color in [Game.COLOR.MAROON, Game.COLOR.FOREST, Game.COLOR.NAVY])
		noteProblem(&"C4", &"AuraBreakerColor", component, color in [Game.COLOR.ICE, Game.COLOR.MUD, Game.COLOR.GRAFFITI])

func noteProblem(mod:StringName, type:StringName, component:GameComponent, isProblem:bool) -> void:
	var problem:Array = [mod, type]
	if isProblem and problem not in component.problems:
		component.problems.append(problem)
		Mods.mods[mod].problems[type].append(component)
		problems += 1
		if isReady: problemDisplays[mod][type].newInstance()
	elif !isProblem and problem in component.problems:
		component.problems.erase(problem)
		var index = Mods.mods[mod].problems[type].find(component)
		Mods.mods[mod].problems[type].remove_at(index)
		problems -= 1
		if isReady: problemDisplays[mod][type].removeInstance(index)
	if isReady: Mods.mods[mod].selectButton.setIcon()

func componentRemoved(component:GameComponent) -> void:
	for problem in component.problems:
		noteProblem(problem[0], problem[1], component, false)

class ModSelectButton extends Button:
	const NO_PROBLEM:Texture2D = preload("res://assets/ui/mods/noProblem.png")
	const PROBLEM:Texture2D = preload("res://assets/ui/mods/problem.png")

	var findProblems:FindProblems
	var modId:StringName
	var mod:Mods.Mod

	func _init(_findProblems:FindProblems, _modId:StringName) -> void:
		toggle_mode = true
		findProblems = _findProblems
		button_group = findProblems.buttonGroup
		modId = _modId
		mod = Mods.mods[modId]
		mod.selectButton = self
		text = mod.name
		setIcon()
		theme_type_variation = &"RadioButtonText"
		if findProblems.firstButton:
			button_pressed = true
			findProblems.firstButton = false

	func setIcon() -> void:
		if mod.hasProblems(): icon = PROBLEM
		else: icon = NO_PROBLEM
