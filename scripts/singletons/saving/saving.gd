extends Node

var editor:Editor

enum ACTION {NEW, OPEN, SAVE_FOR_PLAY, NONE}

var savePath:String = ""
var confirmAction:ACTION

var jsCallback:JavaScriptObject

const FILE_FORMAT_VERSION:int = 1

# Okay.
# Here's how we'll do it
# HEADER:
# - file format header
# - file format version number
# - last opened editor version
# LEVEL METADATA:
# - level object
# - screenshot
# - active mods
# - modpack
# - modpack version
# - levelstart
# LEVEL DATA:
# - tiles
# - components
# - objects

func _ready() -> void:
	if OS.has_feature('web'):
		JavaScriptBridge.eval("window.callbacks = {loadJs: null};")

func editorReady() -> void:
	if !OS.has_feature('web'):
		DirAccess.make_dir_absolute("user://puzzles")
		DirAccess.make_dir_absolute("user://exports")
	editor.saveAsDialog.add_filter("*.cedit", "IWLCEditor Puzzle File")
	editor.openDialog.add_filter("*.cedit", "IWLCEditor Puzzle File")
	editor.unsavedChangesPopup.get_ok_button().theme_type_variation = &"RadioButtonText"
	editor.unsavedChangesPopup.get_cancel_button().theme_type_variation = &"RadioButtonText"
	editor.unsavedChangesPopup.get_ok_button().pressed.connect(confirmed)
	editor.loadErrorPopup.get_ok_button().theme_type_variation = &"RadioButtonText"
	editor.saveAsDialog.file_selected.connect(save)
	editor.openDialog.file_selected.connect(loadFile)

func open() -> void:
	confirmAction = ACTION.OPEN
	if Game.anyChanges:
		editor.unsavedChangesPopup.position = get_window().position+(get_window().size-editor.unsavedChangesPopup.size)/2
		editor.unsavedChangesPopup.visible = true
		editor.unsavedChangesPopup.grab_focus()
	else: confirmed()

func saveAs() -> void:
	editor.saveAsDialog.current_dir = "puzzles"
	editor.saveAsDialog.current_file = "puzzles/"+Game.level.name+".cedit"
	editor.saveAsDialog.visible = true
	editor.saveAsDialog.grab_focus()

func new() -> void:
	confirmAction = ACTION.NEW
	if Game.anyChanges:
		editor.unsavedChangesPopup.position = get_window().position+(get_window().size-editor.unsavedChangesPopup.size)/2
		editor.unsavedChangesPopup.visible = true
		editor.unsavedChangesPopup.grab_focus()
	else: confirmed()

func confirmed() -> void:
	match confirmAction:
		ACTION.NEW: clear()
		ACTION.OPEN:
			if OS.has_feature('web'):
				jsCallback = JavaScriptBridge.create_callback(loadJs)
				JavaScriptBridge.get_interface("callbacks").loadJs = jsCallback
				JavaScriptBridge.eval("
					const input = document.createElement('input');
					input.setAttribute('type', 'file');
					input.setAttribute('accept', '.cedit');
					input.addEventListener('change', event=>{
						const reader = new FileReader();
						reader.onload = ()=>{
							let array = new Uint8Array(reader.result);
							callbacks.loadJs(array.toBase64());
						}
						reader.readAsArrayBuffer(event.target.files[0]);
						input.remove();
					});
					input.click();
				")
			else:
				editor.openDialog.current_dir = "puzzles"
				editor.openDialog.visible = true
				editor.openDialog.grab_focus()

func clear() -> void:
	savePath = ""
	if editor:
		editor.focusDialog.defocus()
		editor.objectHovered = null
		editor.componentHovered = null
		editor.componentDragged = null
		editor.lockBufferConvert = false
		editor.connectionSource = null
		if editor.modsWindow: editor.modsWindow._close()
		editor.quickSet.applyOrCancel()
		editor.modes.setMode(Editor.MODE.SELECT)
		editor.otherObjects.objectSelected(PlayerSpawn, true)
		editor.multiselect.deselect()
		editor.multiselect.clipboard.clear()
		editor.paste.disabled = true
	if Game.playState != Game.PLAY_STATE.EDIT: await Game.stopTest()
	Game.latestSpawn = null
	Game.levelStart = null
	Game.fastAnimSpeed = 0
	Game.fastAnimTimer = 0
	Game.complexViewHue = 0
	Game.goldIndexFloat = 0
	Game.objectIdIter = 0
	Game.componentIdIter = 0
	for object in Game.objects.values(): object.queue_free()
	Game.objects.clear()
	for component in Game.components.values(): component.queue_free()
	Game.components.clear()
	Game.level = Level.new()
	Game.anyChanges = false
	Game.tiles.clear()
	Game.tilesDropShadow.clear()
	Changes.undoStack.clear()
	Changes.undoStack.append(Changes.UndoSeparator.new())
	Changes.stackPosition = 0
	Mods.activeModpack = Mods.modpacks[&"Refactored"]
	Mods.activeVersion = Mods.activeModpack.versions[0]
	for mod in Mods.mods.values(): mod.active = false
	Mods.bufferModsChanged()
	Game.level.activate()
	if editor: editor.home()

func save(path:String="") -> void:
	if OS.has_feature('web'):
		savePath = "user://temp.cedit"
		path = savePath
	elif !path:
		if savePath:
			path = savePath
			if !Game.anyChanges:
				if confirmAction == ACTION.SAVE_FOR_PLAY: Game.playSaved()
				return
		else: return saveAs()
	else: savePath = path
	var prevAnyChanges = Game.anyChanges
	Game.anyChanges = false

	var file:FileAccess = FileAccess.open(path,FileAccess.ModeFlags.WRITE)
	if !file:
		print(FileAccess.get_open_error())
		Game.anyChanges = prevAnyChanges
		return

	# HEADER
	file.store_pascal_string("IWLCEditorLevel")
	file.store_32(FILE_FORMAT_VERSION)
	file.store_pascal_string(ProjectSettings.get_setting("application/config/version"))
	# LEVEL METADATA
	file.store_var(Game.level,true)
	await editor.takeScreenshot()
	file.store_var(editor.screenshot,true)
	file.store_var(Mods.getActiveMods())
	var modpackId = Mods.modpacks.find_key(Mods.activeModpack)
	file.store_var(modpackId if modpackId else &"")
	if Mods.activeModpack: file.store_32(Mods.activeModpack.versions.find(Mods.activeVersion))
	file.store_64(Game.levelStart.id if Game.levelStart else -1)
	# LEVEL DATA
	# tiles
	file.store_var(Game.tiles.tile_map_data)
	# components
	file.store_64(Game.componentIdIter)
	file.store_64(len(Game.components))
	for component in Game.components.values():
		file.store_16(Game.COMPONENTS.find(component.get_script()))
		for property in component.PROPERTIES:
			file.store_var(component.get(property), true)
		for array in component.ARRAYS.keys():
			if arrayTypeIsComponent(component.ARRAYS[array]): file.store_var(componentArrayToIDs(component.get(array)))
			else: file.store_var(component.get(array))
	# objects
	file.store_64(Game.objectIdIter)
	file.store_64(len(Game.objects))
	for object in Game.objects.values():
		if object is PlaceholderObject: continue
		file.store_16(Game.COMPONENTS.find(object.get_script()))
		for property in object.PROPERTIES:
			if object is PlayerSpawn and property == &"undoStack":
				file.store_var(SerialisedUndoStack.new(object.undoStack) if object.undoStack else null, true)
			else: file.store_var(object.get(property), true)
		for array in object.ARRAYS.keys():
			if arrayTypeIsComponent(object.ARRAYS[array]): file.store_var(componentArrayToIDs(object.get(array)))
			else: file.store_var(object.get(array))
		if object is Door: file.store_var(componentArrayToIDs(object.locks))
		elif object is KeyCounter: file.store_var(componentArrayToIDs(object.elements))
	file.close()
	if OS.has_feature('web') and confirmAction != ACTION.SAVE_FOR_PLAY:
		JavaScriptBridge.download_buffer(FileAccess.get_file_as_bytes(path),Game.level.name+".cedit")
	
	if confirmAction == ACTION.SAVE_FOR_PLAY: Game.playSaved()

func arrayTypeIsComponent(arrayType) -> bool: return arrayType is GDScript and arrayType in Game.COMPONENTS

func componentArrayToIDs(array:Array) -> Array: return array.map(func(component):return component.id)
func IDArraytoComponents(type:GDScript,array:Array) -> Array:
	if type in Game.NON_OBJECT_COMPONENTS: return array.map(func(id):return Game.components[id])
	else: return array.map(func(id):return Game.objects[id])

func loadFile(path:String, immediate:bool=false) -> OpenWindow:
	var openWindow:OpenWindow = preload("res://scenes/openWindow.tscn").instantiate()
	openWindow.position = get_window().position+(get_window().size-openWindow.size)/2
	openWindow.path = path

	if path.get_extension() != "cedit": errorPopup("Unrecognised file format"); return null

	var file:FileAccess = FileAccess.open(path,FileAccess.ModeFlags.READ)

	if file.get_pascal_string() != "IWLCEditorLevel": errorPopup("Unrecognised file format"); return null
	var formatVersion:int = file.get_32()
	var editorVersion:String = file.get_pascal_string()
	if formatVersion == 1: openWindow.loader = LoadV1
	else:
		openWindow.queue_free()
		if formatVersion == 0: errorPopup("File version 0 is unrecognised")
		else: errorPopup("File version %s is unrecognised. File last opened in IWLCEditor v%s" % [formatVersion, editorVersion])
		return null

	openWindow.level = file.get_var(true)
	openWindow.screenshot = file.get_var(true)
	openWindow.mods = file.get_var()
	var modpackId:StringName = file.get_var()
	if modpackId:
		openWindow.modpack = Mods.modpacks[modpackId]
		openWindow.version = Mods.modpacks[modpackId].versions[file.get_32()]
	openWindow.levelStart = file.get_64()
	openWindow.file = file
	if immediate: openWindow.resolve()
	else: editor.add_child(openWindow)
	return openWindow

func loadJs(result) -> void:
	var buffer:PackedByteArray = Marshalls.base64_to_raw(result[0])
	var file = FileAccess.open("user://temp.cedit",FileAccess.ModeFlags.WRITE)
	file.store_buffer(buffer)
	file.close()
	loadFile("user://temp.cedit")

func errorPopup(message:String,title:="Load Error") -> void:
	editor.loadErrorPopup.title = title
	editor.loadErrorPopup.dialog_text = message
	editor.loadErrorPopup.position = get_window().position+(get_window().size-editor.loadErrorPopup.size)/2
	editor.loadErrorPopup.visible = true
	editor.loadErrorPopup.grab_focus()

func openExportWindow() -> void:
	if editor.exportWindow:
		editor.exportWindow.grab_focus()
	else:
		var window:Window = preload("res://scenes/exportWindow.tscn").instantiate()
		editor.add_child(window)
		window.position = get_window().position+(get_window().size-window.size)/2
