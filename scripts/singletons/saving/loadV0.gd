extends Node
class_name LoadV0

static var COMPONENTS:Array[GDScript] = [Lock, KeyCounterElement, KeyBulk, Door, Goal, KeyCounter, PlayerSpawn, FloatingTile, RemoteLock]
static var NON_OBJECT_COMPONENTS:Array[GDScript] = [Lock, KeyCounterElement]

static var PROPERTIES:Dictionary[GDScript,Array] = {
	Lock: [
		&"id", &"position", &"size",
		&"parentId", &"color", &"type", &"sizeType", &"count", &"configuration", &"zeroI", &"isPartial", &"denominator", &"negated", &"armament",
		&"index", &"displayIndex"
	],
	KeyCounterElement: [
		&"id", &"position", &"size",
		&"parentId", &"color",
		&"index"
	],
	KeyBulk: [
		&"id", &"position", &"size",
		&"color", &"type", &"count", &"infinite", &"un"
	],
	Door: [
		&"id", &"position", &"size",
		&"colorSpend", &"copies", &"infCopies", &"type",
		&"frozen", &"crumbled", &"painted"
	],
	Goal: [
		&"id", &"position", &"size",
		&"type"
	],
	KeyCounter: [
		&"id", &"position", &"size",
	],
	PlayerSpawn: [
		&"id", &"position", &"size",
	],
	FloatingTile: [
		&"id", &"position", &"size",
	],
	RemoteLock: [
		&"id", &"position", &"size",
		&"color", &"type", &"configuration", &"sizeType", &"count", &"zeroI", &"isPartial", &"denominator", &"negated", &"armament",
		&"frozen", &"crumbled", &"painted"
	]
}
static var ARRAYS:Dictionary[GDScript,Dictionary] = {
	Lock: {},
	KeyCounterElement: {},
	KeyBulk: {},
	Door: {&"remoteLocks":RemoteLock},
	Goal: {},
	KeyCounter: {},
	PlayerSpawn: {&"key":TYPE_PACKED_INT64_ARRAY,&"star":TYPE_BOOL,&"curse":TYPE_BOOL},
	FloatingTile: {},
	RemoteLock: {&"doors":Door},
}

# LEVEL METADATA:
# - level name
# - level description
# - level author
# - level size
# - active mods
# - modpack
# - modpack version
# LEVEL DATA:
# - tiles
# - components
# - objects

static func loadFile(file:FileAccess) -> void:
	#Game.level = file.get_var(true)
	#Game.levelBounds.size = file.get_var()
	#for mod in file.get_var(): Mods.mods[mod].active = true
	#var modpackId:StringName = file.get_var()
	#if modpackId:
	#	Mods.activeModpack = Mods.modpacks[modpackId]
	#	Mods.activeVersion = Mods.activeModpack.versions[file.get_32()]
	#else:
	#	Mods.activeModpack = null
	#	Mods.activeVersion = null
	#var levelStart:int = file.get_64()
	# LEVEL DATA
	# tiles
	Game.tiles.tile_map_data = file.get_var()
	Game.tilesDropShadow.tile_map_data = Game.tiles.tile_map_data
	# components
	Game.componentIdIter = file.get_64()
	var componentBufferedArrays:Dictionary[int,Dictionary] = {} # dictionary[object id, dictionary[property name, array]]
	for _i in file.get_64():
		var type:GDScript = COMPONENTS[file.get_16()]
		var component = type.new()
		if Game.editor: component.editor = Game.editor
		for property in PROPERTIES[type]:
			var value = file.get_var(true)
			if property == &"id":
				Game.components[value] = component
			component.set(property, value)
			component.propertyChangedDo(property)
		for array in ARRAYS[type].keys():
			componentBufferedArrays[component.id][array] = file.get_var() # handle it at the end; not all components will be ready
	# objects
	Game.objectIdIter = file.get_64()
	var objectBufferedArrays:Dictionary[int,Dictionary] = {} # dictionary[object id, dictionary[property name, array]]
	for _i in file.get_64():
		var type:GDScript = COMPONENTS[file.get_16()]
		var object = type.SCENE.instantiate()
		if Game.editor: object.editor = Game.editor
		for property in PROPERTIES[type]:
			var value = file.get_var(true)
			if property == &"id":
				Game.objects[value] = object
				Game.objectsParent.add_child(object)
			object.set(property, value)
			object.propertyChangedDo(property)
		objectBufferedArrays[object.id] = {}
		for array in ARRAYS[type].keys():
			objectBufferedArrays[object.id][array] = file.get_var() # handle it at the end
		if type == Door:
			object.locks.assign(Saving.IDArraytoComponents(Lock, file.get_var()))
			for lock in object.locks:
				lock.parent = object
				object.add_child(lock)
			object.reindexLocks()
		if type == KeyCounter:
			object.elements.assign(Saving.IDArraytoComponents(KeyCounterElement, file.get_var()))
			for element in object.elements:
				element.parent = object
				object.add_child(element)
	
	for componentId in componentBufferedArrays.keys():
		var component:GameComponent = Game.components[componentId]
		for array in ARRAYS[component.get_script()]:
			var value:Array = componentBufferedArrays[componentId][array]
			var arrayType:Variant = ARRAYS[component.get_script()][array]
			if arrayType in Game.COMPONENTS: value = Saving.IDArraytoComponents(arrayType,value)
			component.get(array).assign(value)

	for objectId in objectBufferedArrays.keys():
		var object:GameObject = Game.objects[objectId]
		for array in ARRAYS[object.get_script()]:
			var value:Array = objectBufferedArrays[objectId][array]
			var arrayType:Variant = ARRAYS[object.get_script()][array]
			if arrayType in Game.COMPONENTS: value = Saving.IDArraytoComponents(arrayType,value)
			object.get(array).assign(value)

	#if levelStart != -1:
	#	Game.levelStart = Game.objects[levelStart]
	#	if Game.editor: Game.editor.topBar._updateButtons()
	
	Game.updateWindowName()
	if Game.editor:
		Game.editor.settingsMenu.opened()
	Game.get_tree().call_group("modUI", "changedMods")
