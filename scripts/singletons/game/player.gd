extends CharacterBody2D
class_name Player

const HELD_SHINE:Texture2D = preload("res://assets/game/player/held/shine.png")
func getMasterShineColor() -> Color: return Color("#b4b432") if masterMode.sign() > 0 else Color("#3232b4")

const HELD_MASTER:Texture2D = preload("res://assets/game/player/held/master.png")
const HELD_QUICKSILVER:Texture2D = preload("res://assets/game/player/held/quicksilver.png")
const HELD_MASTER_NEGATIVE:Texture2D = preload("res://assets/game/player/held/masterNegative.png")
const HELD_QUICKSILVER_NEGATIVE:Texture2D = preload("res://assets/game/player/held/quicksilverNegative.png")
func getHeldKeySprite() -> Texture2D:
	if masterCycle == 1: return HELD_MASTER if masterMode.sign() > 0 else HELD_MASTER_NEGATIVE
	else: return HELD_QUICKSILVER if masterMode.sign() > 0 else HELD_QUICKSILVER_NEGATIVE

const AURA_RED:Texture2D = preload("res://assets/game/player/aura/red.png")
const AURA_GREEN:Texture2D = preload("res://assets/game/player/aura/green.png")
const AURA_BLUE:Texture2D = preload("res://assets/game/player/aura/blue.png")
const AURA_MAROON:Texture2D = preload("res://assets/game/player/aura/maroon.png")
const AURA_FOREST:Texture2D = preload("res://assets/game/player/aura/forest.png")
const AURA_NAVY:Texture2D = preload("res://assets/game/player/aura/navy.png")
const AURA_DRAW_OPACITY:Color = Color(Color.WHITE,0.5)
const AURA_RECT:Rect2 = Rect2(Vector2(-32,-32),Vector2(64,64))

const FPS:float = 60 # godot velocity works in /s so we account for gamemaker's fps, which is 60

const JUMP_SPEED:float = 8.5
const DOUBLE_JUMP_SPEED:float = 7
const GRAVITY:float = 0.4
const Y_MAXSPEED:float = 9

var canDoubleJump:bool = true
var key:Array[C] = []
var star:Array[bool]
var curse:Array[bool]

var cantSave:bool = false # cant save if near a door

var masterMode:C = C.ZERO
var masterCycle:int = 0 # 0 = None, 1 = Master, 2 = Silver
const MASTER_CYCLE_COLORS:Array[Game.COLOR] = [Game.COLOR.WHITE, Game.COLOR.MASTER, Game.COLOR.QUICKSILVER]

var complexMode:C = C.ONE # C(1,0) for real view, C(0,1) for i-view

var masterShineDraw:RID
var masterKeyDraw:RID
var masterShineAngle:float = 0

var pauseFrame:bool = true # jank prevention

var auraRed:bool = false
var auraGreen:bool = false
var auraBlue:bool = false
var auraMaroon:bool = false
var auraForest:bool = false
var auraNavy:bool = false
var auraDraw:RID

var explodey:bool = false

var curseMode:int = 0 # 0 = none, 1 = curse, -1 = uncurse
var curseColor:Game.COLOR
var drawCurse:CurseParticle

var complexModeTextDraw:RID
var complexSwitchDraw:RID
var complexSwitchAnim:bool = false
var complexSwitchAngle:float = 0

var previousPosition:Vector2
var previousIsOnFloor:bool

const WARP_1:Texture2D = preload("res://assets/game/player/lily/warp1.png")
const WARP_2:Texture2D = preload("res://assets/game/player/lily/warp2.png")
var warpDraw:RID
var crashAnimAngle:float = 0
var crashAnimHue:float = 0
var crashAnimSat:float = 0
var crashAnimVal:float = 0

func _ready() -> void:
	warpDraw = RenderingServer.canvas_item_create()
	auraDraw = RenderingServer.canvas_item_create()
	masterShineDraw = RenderingServer.canvas_item_create()
	masterKeyDraw = RenderingServer.canvas_item_create()
	complexModeTextDraw = RenderingServer.canvas_item_create()
	complexSwitchDraw = RenderingServer.canvas_item_create()
	drawCurse = CurseParticle.new(curseColor,curseMode)
	RenderingServer.canvas_item_set_material(masterShineDraw, Game.ADDITIVE_MATERIAL)
	RenderingServer.canvas_item_set_material(complexSwitchDraw, Game.ADDITIVE_MATERIAL)
	drawCurse.z_index = 4
	RenderingServer.canvas_item_set_z_index(warpDraw,2)
	RenderingServer.canvas_item_set_z_index(auraDraw,6)
	RenderingServer.canvas_item_set_z_index(masterShineDraw,6)
	RenderingServer.canvas_item_set_z_index(masterKeyDraw,6)
	RenderingServer.canvas_item_set_z_index(complexModeTextDraw,6)
	RenderingServer.canvas_item_set_z_index(complexSwitchDraw,6)
	RenderingServer.canvas_item_set_parent(warpDraw, get_canvas_item())
	RenderingServer.canvas_item_set_parent(auraDraw, get_canvas_item())
	RenderingServer.canvas_item_set_parent(masterShineDraw, get_canvas_item())
	RenderingServer.canvas_item_set_parent(masterKeyDraw, get_canvas_item())
	RenderingServer.canvas_item_set_parent(complexModeTextDraw, get_canvas_item())
	RenderingServer.canvas_item_set_parent(complexSwitchDraw, get_canvas_item())
	add_child(drawCurse)

	for color in Game.COLORS:
		# if color == Game.COLOR.STONE:
		key.append(C.ZERO)
		star.append(false)
		curse.append(color == Game.COLOR.BROWN)
	
	previousPosition = position
	previousIsOnFloor = is_on_floor()

func paused() -> bool:
	return Game.playState != Game.PLAY_STATE.PLAY or (Game.playGame and (Game.playGame.inAnimation() or Game.playGame.paused)) or Game.won or Game.crashState

func _physics_process(_delta:float) -> void:
	if paused():
		if Game.won: visible = false
		else: %sprite.pause()
		return
	
	var xSpeed:float = 6
	if Input.is_key_pressed(KEY_CTRL): xSpeed = 1
	elif !is_on_floor() or (Input.is_key_pressed(KEY_SHIFT) == Game.autoRun): xSpeed = 3
	var moveDirection:float = Input.get_axis(&"left", &"right")
	velocity.x = xSpeed*FPS*moveDirection

	if pauseFrame:
		pauseFrame = false
	else:
		cantSave = false
		for area in %near.get_overlapping_areas(): near(area)
		for area in %interact.get_overlapping_areas(): interacted(area)
		GameChanges.process()
		previousPosition = position
		previousIsOnFloor = is_on_floor()

	var onAnything:bool = Game.tiles in %floor.get_overlapping_bodies()
	for area in %floor.get_overlapping_areas():
		if area.get_parent() is Door:
			if area.get_parent().type == Door.TYPE.COMBO or !area.get_parent().justOpened: onAnything = true
	if is_on_floor() and onAnything:
		canDoubleJump = true

	if Input.is_action_just_pressed(&"jump"):
		if is_on_floor():
			velocity.y = -JUMP_SPEED*FPS
			AudioManager.play(preload("res://resources/sounds/player/jump.wav"))
			if !onAnything:
				velocity.y *= 0.45
				canDoubleJump = false
		elif canDoubleJump:
			velocity.y = -DOUBLE_JUMP_SPEED*FPS
			canDoubleJump = false
			AudioManager.play(preload("res://resources/sounds/player/doubleJump.wav"))
	if Input.is_action_just_released(&"jump") and velocity.y < 0 and !Game.fullJumps: velocity.y *= 0.45
	velocity.y += GRAVITY*FPS
	velocity.y = clamp(velocity.y, -Y_MAXSPEED*FPS, Y_MAXSPEED*FPS)

	move_and_slide()

	if moveDirection: %sprite.flip_h = moveDirection < 0

	if velocity.y <= -0.05*FPS: %sprite.play("jump")
	elif velocity.y >= 0.05*FPS: %sprite.play("fall")
	elif moveDirection: %sprite.play("run")
	else: %sprite.play("idle")

func _process(delta:float) -> void:
	masterShineAngle += delta*4.1887902048 # 4 degrees per frame, 60fps
	masterShineAngle = fmod(masterShineAngle,TAU)
	if complexSwitchAnim:
		complexSwitchAngle += delta*5.2359877560 # 5 degrees per frame, 60fps
		if complexSwitchAngle >= 1.5707963268: complexSwitchAnim = false
	if Game.crashState:
		crashAnimAngle += delta*4.7123889804 # 4.5 degrees per frame, 60fps
		crashAnimHue += delta*2.3529411765
		if crashAnimHue >= 1: crashAnimHue -= 1
		crashAnimVal = min(crashAnimHue+15*delta,1)
		crashAnimSat = min(crashAnimSat+1.5*delta,1)
	queue_redraw()

	drawCurse.mode = curseMode
	drawCurse.color = curseColor
	drawCurse.queue_redraw()

func receiveKey(event:InputEventKey):
	if event.echo or paused(): return
	match event.keycode:
		KEY_P: if Game.editor: Game.pauseTest()
		KEY_O: if Game.editor: Game.stopTest()
		KEY_R: Game.restart()
		KEY_Z: if GameChanges.undo(): AudioManager.play(preload("res://resources/sounds/player/undo.wav"), 1, 0.6)
		KEY_X: cycleMaster()
		KEY_S: complexSwitch()
		KEY_U: print(GameChanges.undoStack)

func _newlyInteracted(area:Area2D) -> void:
	if pauseFrame: return
	var object:GameObject = area.get_parent()
	if object is KeyBulk: object.collect(self)
	elif object is RemoteLock: object.check(self)
	elif object is Door and object.type == Door.TYPE.GATE: checkKeys()
	elif object is Goal: Game.win(object)

func _newlyUninteracted(area: Area2D):
	if pauseFrame: return
	var object:GameObject = area.get_parent()
	if object is Door and object.type == Door.TYPE.GATE: checkKeys()

func interacted(area:Area2D) -> void:
	var object:GameObject = area.get_parent()
	if object is Door:
		if object.justOpened: object.justOpened = false
		object.tryOpen(self)
	elif object is KeyBulk:
		cantSave = true

func near(area:Area2D) -> void:
	var object:GameObject = area.get_parent()
	if object is Door:
		cantSave = true
		object.auraCheck(self)
		if curseMode: object.curseCheck(self)
	if object is RemoteLock:
		cantSave = true
		object.auraCheck(self)
		if curseMode: object.curseCheck(self)

func overlapping(area:Area2D) -> bool: return %interact.overlaps_area(area)

func cycleMaster() -> void:
	var armamentImmunities:Array[Game.COLOR] = getArmamentImmunities()

	if masterCycle < 1 and Game.COLOR.MASTER not in armamentImmunities: # MASTER
		var relevantCount:C = key[Game.COLOR.MASTER].across(complexMode)
		if relevantCount.neq(0):
			masterCycle = 1
			masterMode = relevantCount.axis()
			if relevantCount.sign() > 0: AudioManager.play(preload("res://resources/sounds/player/masterEquip.wav"))
			else: AudioManager.play(preload("res://resources/sounds/player/masterNegativeEquip.wav"))
			return
	if masterCycle < 2 and Game.COLOR.QUICKSILVER not in armamentImmunities: # QUICKSILVER
		var relevantCount:C = key[Game.COLOR.QUICKSILVER].across(complexMode)
		if relevantCount.neq(0):
			masterCycle = 2
			masterMode = relevantCount.axis()
			if relevantCount.sign() > 0: AudioManager.play(preload("res://resources/sounds/player/masterEquip.wav"))
			else: AudioManager.play(preload("res://resources/sounds/player/masterNegativeEquip.wav"))
			return
	if masterCycle != 0:
		AudioManager.play(preload("res://resources/sounds/player/masterUnequip.wav"))
	dropMaster()

func dropMaster() -> void:
	masterMode = C.ZERO
	masterCycle = 0

func getArmamentImmunities() -> Array[Game.COLOR]:
	var colors:Array[Game.COLOR] = []
	for area in %interact.get_overlapping_areas():
		var object = area.get_parent()
		if object is Door and object.type == Door.TYPE.GATE:
			for color in object.armamentColors():
				if color not in colors: colors.append(color)
	return colors

func checkKeys() -> void:
	var armamentImmunities:Array[Game.COLOR] = getArmamentImmunities()

	match masterCycle:
		1: if !key[Game.COLOR.MASTER].across(masterMode).sign() > 0 or Game.COLOR.MASTER in armamentImmunities: dropMaster()
		2: if !key[Game.COLOR.QUICKSILVER].across(masterMode).sign() > 0 or Game.COLOR.QUICKSILVER in armamentImmunities: dropMaster()

	auraRed = key[Game.COLOR.RED].gt(0) and !key[Game.COLOR.RED].minus(key[Game.COLOR.MAROON]).lt(1) and Game.COLOR.RED not in armamentImmunities
	auraGreen = key[Game.COLOR.GREEN].gt(0) and !key[Game.COLOR.GREEN].minus(key[Game.COLOR.FOREST]).lt(5) and Game.COLOR.GREEN not in armamentImmunities
	auraBlue = key[Game.COLOR.BLUE].gt(0) and !key[Game.COLOR.BLUE].minus(key[Game.COLOR.NAVY]).lt(3) and Game.COLOR.BLUE not in armamentImmunities
	auraMaroon = key[Game.COLOR.MAROON].gt(0) and !key[Game.COLOR.MAROON].minus(key[Game.COLOR.RED]).lt(1) and Game.COLOR.MAROON not in armamentImmunities
	auraForest = key[Game.COLOR.FOREST].gt(0) and !key[Game.COLOR.FOREST].minus(key[Game.COLOR.GREEN]).lt(5) and Game.COLOR.FOREST not in armamentImmunities
	auraNavy = key[Game.COLOR.NAVY].gt(0) and !key[Game.COLOR.NAVY].minus(key[Game.COLOR.BLUE]).lt(3) and Game.COLOR.NAVY not in armamentImmunities

	explodey = key[Game.COLOR.DYNAMITE].neq(0) and Game.COLOR.DYNAMITE not in armamentImmunities

	curseMode = 0
	var highestSeen:Q = Q.ZERO
	if Game.COLOR.PURE not in armamentImmunities:
		for color in Game.COLORS:
			if !curse[color] or key[color].r.eq(0) or color in armamentImmunities: continue
			# tie
			if key[color].r.abs().eq(highestSeen): curseMode = 0
			elif key[color].r.abs().gt(highestSeen):
				highestSeen = key[color].r.abs()
				curseMode = key[color].r.sign()
				curseColor = color as Game.COLOR

func complexSwitch() -> void:
	if complexMode.eq(1): complexMode = C.I
	else: complexMode = C.ONE

	AudioManager.play(preload("res://resources/sounds/player/camera.wav"))
	AudioManager.play(preload("res://resources/sounds/key/signflip.wav"))
	complexSwitchAnim = true
	complexSwitchAngle = 0

	if complexMode.eq(C.I) and masterCycle and key[MASTER_CYCLE_COLORS[masterCycle]].across(C.I).neq(0):
		masterMode = key[MASTER_CYCLE_COLORS[masterCycle]].across(C.I).axis()
	elif complexMode.eq(1) and masterCycle and key[MASTER_CYCLE_COLORS[masterCycle]].across(1).neq(0):
		masterMode = key[MASTER_CYCLE_COLORS[masterCycle]].across(1).axis()
	elif masterCycle:
		AudioManager.play(preload("res://resources/sounds/player/masterUnequip.wav"))
		dropMaster()
	for object in Game.objects.values(): if object is Door: object.complexCheck()
	for component in Game.components.values(): if component is Lock: component.queue_redraw()

func crashAnim() -> void:
	%sprite.visible = false
	crashAnimAngle = 0
	crashAnimHue = 0
	crashAnimSat = 0
	crashAnimVal = 0.3137254902

func _draw() -> void:
	RenderingServer.canvas_item_clear(warpDraw)
	RenderingServer.canvas_item_clear(auraDraw)
	RenderingServer.canvas_item_clear(masterShineDraw)
	RenderingServer.canvas_item_clear(masterKeyDraw)
	RenderingServer.canvas_item_clear(complexModeTextDraw)
	RenderingServer.canvas_item_clear(complexSwitchDraw)
	# warps
	if Game.crashState:
		var offset:int = int(12*sin(crashAnimAngle))
		RenderingServer.canvas_item_add_texture_rect(warpDraw,Rect2(Vector2(-16+offset,-23),Vector2(32,32)),WARP_1,false,Color.from_hsv(crashAnimHue,crashAnimSat,crashAnimVal))
		RenderingServer.canvas_item_add_texture_rect(warpDraw,Rect2(Vector2(-16-offset,-23),Vector2(32,32)),WARP_2,false,Color.from_hsv(crashAnimHue,crashAnimSat,crashAnimVal))
		return
	# auras
	if auraRed: RenderingServer.canvas_item_add_texture_rect(auraDraw,AURA_RECT,AURA_RED,false,AURA_DRAW_OPACITY)
	if auraMaroon: RenderingServer.canvas_item_add_texture_rect(auraDraw,AURA_RECT,AURA_MAROON,false,AURA_DRAW_OPACITY)
	if auraGreen: RenderingServer.canvas_item_add_texture_rect(auraDraw,AURA_RECT,AURA_GREEN,false,AURA_DRAW_OPACITY)
	if auraForest: RenderingServer.canvas_item_add_texture_rect(auraDraw,AURA_RECT,AURA_FOREST,false,AURA_DRAW_OPACITY)
	if auraBlue: RenderingServer.canvas_item_add_texture_rect(auraDraw,AURA_RECT,AURA_BLUE,false,AURA_DRAW_OPACITY)
	if auraNavy: RenderingServer.canvas_item_add_texture_rect(auraDraw,AURA_RECT,AURA_NAVY,false,AURA_DRAW_OPACITY)
	# held
	if masterCycle != 0:
		var masterShineScale:float = 0.8 + 0.2*sin(masterShineAngle)
		var masterDrawOpacity:Color = Color(Color.WHITE,masterShineScale*0.6)
		RenderingServer.canvas_item_add_texture_rect(masterShineDraw,Rect2(Vector2(-32,-32)*masterShineScale,Vector2(64,64)*masterShineScale),HELD_SHINE,false,getMasterShineColor())
		RenderingServer.canvas_item_add_texture_rect(masterKeyDraw,Rect2(Vector2(-16,-16),Vector2(32,32)),getHeldKeySprite(),false,masterDrawOpacity)
	if complexMode.eq(C.I):
		TextDraw.outlinedCentered(Game.FTALK,complexModeTextDraw,"I-View",Color.from_hsv(Game.complexViewHue,0.4901960784,1),Color.BLACK,12,Vector2(0,-10))
	# complex switch
	if complexSwitchAnim:
		var switchScale:float = sin(complexSwitchAngle)
		RenderingServer.canvas_item_add_texture_rect(complexSwitchDraw,Rect2(Vector2(-64*switchScale,-64*switchScale),Vector2(128*switchScale,128*switchScale)),CurseParticle.TEXTURE_GENERIC,false,Color(Color.WHITE,cos(complexSwitchAngle)))
