extends PanelContainer
class_name PlayGame

const SCREEN_RECT:Rect2 = Rect2(Vector2.ZERO,Vector2(800,608))
const DESCRIPTION_BOX:Texture2D = preload("res://assets/game/gameUI/description.png")

const TEXT_BREAK_FLAGS:int = TextServer.LineBreakFlag.BREAK_MANDATORY|TextServer.LineBreakFlag.BREAK_WORD_BOUND|TextServer.LineBreakFlag.BREAK_ADAPTIVE

@onready var world:World = %world
@onready var gameViewport:SubViewport = %gameViewport

var configFile:ConfigFile = ConfigFile.new()

var paused:bool = false

var drawDescription:RID
var drawMain:RID
var drawAutoRunGradient:RID

var roomTransitionPhase:int = -1
var roomTransitionTimer:float = 0
var roomTransitionColor:Color = Color("#5a96c8")
var textWiggleAngle:float = 0
var textOffsetAngle:float = 0
var pauseAnimPhase:int = -1
var pauseAnimTimer:float = 0
var autoRunTimer:float = 2

func _ready() -> void:
	drawDescription = RenderingServer.canvas_item_create()
	drawMain = RenderingServer.canvas_item_create()
	drawAutoRunGradient = RenderingServer.canvas_item_create()
	RenderingServer.canvas_item_set_material(drawAutoRunGradient, Game.TEXT_GRADIENT_MATERIAL)
	RenderingServer.canvas_item_set_parent(drawDescription, %worldViewportCont.get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawMain, %drawParent.get_canvas_item())
	RenderingServer.canvas_item_set_parent(drawAutoRunGradient, %drawParent.get_canvas_item())
	Game.playGame = self
	Game.playReadied()

func _process(delta:float) -> void:
	textWiggleAngle += 5.8643062867*delta # 5.6 degrees per frame, 60fps
	textWiggleAngle = fmod(textWiggleAngle,TAU)
	if roomTransitionPhase != -1:
		roomTransitionTimer += delta
		match roomTransitionPhase:
			0:
				roomTransitionColor.a = 1
				textOffsetAngle = deg_to_rad(min(roomTransitionTimer*60,40)*2.25)
				queue_redraw()
				if roomTransitionTimer >= 2.5:
					roomTransitionTimer = 0
					roomTransitionPhase += 1
			1:
				roomTransitionColor.a = 1 - roomTransitionTimer/0.5833333333
				textOffsetAngle = deg_to_rad(90+roomTransitionTimer*154.2857142857)
				queue_redraw()
				if roomTransitionTimer >= 0.4166666667:
					roomTransitionPhase = -1
	if pauseAnimPhase != -1:
		pauseAnimTimer += delta
		%gameViewportCont.get_material().set_shader_parameter(&"pauseAnimTimer", pauseAnimTimer)
		queue_redraw()
		match pauseAnimPhase:
			0:
				if pauseAnimTimer >= 0.4166666667:
					pauseAnimPhase += 1
					paused = !paused
					%pauseMenu.visible = paused
					%gameViewportCont.get_material().set_shader_parameter(&"darken", !paused)
			1:
				if pauseAnimTimer >= 0.75:
					pauseAnimPhase = -1
					%mouseBlocker.mouse_filter = MOUSE_FILTER_IGNORE
					%gameViewportCont.get_material().set_shader_parameter(&"pauseAnimTimer", 0)
					%gameViewportCont.get_material().set_shader_parameter(&"darken", false)
	if autoRunTimer < 2:
		autoRunTimer += delta
		queue_redraw()
		if autoRunTimer >= 2: autoRunTimer = 2
	if !paused: Game.timer += delta
	var objectHovered:GameObject
	var mouseWorldPosition = %world.get_local_mouse_position()
	for object in Game.objects.values():
		if object.active and Rect2(object.position,object.size).has_point(mouseWorldPosition): objectHovered = object
	%mouseover.describe(objectHovered, %gameViewportCont.get_local_mouse_position()*Vector2(800,608)/%gameViewportCont.size,Vector2(800,608))

func _draw() -> void:
	RenderingServer.canvas_item_clear(drawDescription)
	RenderingServer.canvas_item_clear(drawMain)
	RenderingServer.canvas_item_clear(drawAutoRunGradient)
	# description box
	if Game.level.description:
		RenderingServer.canvas_item_add_texture_rect(drawDescription,Rect2(Vector2(11,519),Vector2(784,80)),DESCRIPTION_BOX,false,Color(Color.BLACK,0.35))
		RenderingServer.canvas_item_add_texture_rect(drawDescription,Rect2(Vector2(8,516),Vector2(784,80)),DESCRIPTION_BOX)
		Game.FTALK.draw_multiline_string(drawDescription,Vector2(16,540),Game.level.description,HORIZONTAL_ALIGNMENT_LEFT,666,12,4,Color("#200020"),TEXT_BREAK_FLAGS)
		TextDraw.outlinedCentered(Game.FROOMNUM,drawDescription,"PUZZLE",Color("#d6cfc9"),Color("#3e2d1c"),20,Vector2(732,539))
		TextDraw.outlinedCentered(Game.FROOMNUM,drawDescription,Game.level.shortNumber,Color("#8c50c8"),Color("#140064"),20,Vector2(733,569))
	# room transition
	if roomTransitionPhase != -1:
		var textOffset = Vector2(0,500*sin(textOffsetAngle)-500)
		var textWiggle:Vector2 = Vector2(sin(textWiggleAngle),cos(textWiggleAngle))*3
		var textWiggle2:Vector2 = Vector2(sin(textWiggleAngle+0.8726646260),cos(textWiggleAngle+0.8726646260))*6
		RenderingServer.canvas_item_add_rect(drawMain,SCREEN_RECT,roomTransitionColor)
		TextDraw.outlinedCentered2(Game.FLEVELID,drawMain,Game.level.number,Color.WHITE,Color.BLACK,24,Vector2(400,216)+textWiggle+textOffset)
		TextDraw.outlinedCentered2(Game.FLEVELNAME,drawMain,Game.level.name,Color.WHITE,Color.BLACK,36,Vector2(400,280)+textWiggle2+textOffset)
		TextDraw.outlinedCentered2(Game.FLEVELNAME,drawMain,Game.level.author,Color.BLACK,Color.WHITE,36,Vector2(400,376)+textWiggle+textOffset)
	var autoRunAlpha:float = abs(sin(autoRunTimer*PI))
	if autoRunAlpha > 0:
		TextDraw.outlinedGradient(Game.FMINIID,drawMain,drawAutoRunGradient,
			"[E] Auto-Run is " + ("on" if Game.autoRun else "off"),
			Color(Color("#e6ffe6") if Game.autoRun else Color("#dcffe6"),autoRunAlpha),
			Color(Color("#e6c896") if Game.autoRun else Color("#64dc8c"),autoRunAlpha),
			Color(Color.BLACK,autoRunAlpha),12,Vector2(4,20)
		)

func _input(event:InputEvent) -> void:
	if event is InputEventKey and event.is_pressed():
		if !event.is_echo():
			if event.keycode == KEY_F5: queue_redraw()
			if roomTransitionPhase == 0 and roomTransitionTimer >= 0.6666666667:
				if event.keycode == KEY_SPACE:
					roomTransitionPhase += 1
					roomTransitionTimer = 0
			if !inAnimation():
				if event.keycode == KEY_ESCAPE: pause()
		if !paused and !inAnimation():
			if event.keycode == KEY_E: autoRun()
			Game.player.receiveKey(event)

func startLevel() -> void:
	start()
	roomTransitionPhase = 0
	roomTransitionTimer = 0

func start() -> void:
	Game.player = preload("res://scenes/player.tscn").instantiate()
	world.add_child(Game.player)
	assert(Game.levelStart)
	Game.player.position = Game.levelStart.position + Vector2(16, 23)
	Game.goldIndexFloat = 0
	GameChanges.start()
	for object in Game.objects.values():
		object.start()
		object.queue_redraw()
	for component in Game.components.values():
		component.start()
		component.queue_redraw()

func restart() -> void:
	Game.player.pauseFrame = true
	Game.player.queue_free()
	for object in Game.objects.values():
		object.stop()
		object.queue_redraw()
	for component in Game.components.values():
		component.stop()
		component.queue_redraw()
	await get_tree().process_frame
	start()

func inAnimation() -> bool:
	if roomTransitionPhase == 0: return true
	if roomTransitionPhase == 1 and roomTransitionTimer < 0.1: return true
	if pauseAnimPhase != -1: return true
	return false

func pause() -> void:
	if inAnimation(): return
	pauseAnimPhase = 0
	pauseAnimTimer = 0
	%gameViewportCont.get_material().set_shader_parameter(&"darken", !paused)
	%mouseBlocker.mouse_filter = MOUSE_FILTER_STOP
	var pauseSound:AudioStreamPlayer = AudioManager.play(preload("res://resources/sounds/pause.wav"))
	pauseSound.volume_linear = 0.85
	pauseSound.pitch_scale = 0.6
	if paused: saveSettings()
	else: loadSettings()

func quit() -> void:
	saveSettings()
	get_tree().quit()

func editLevel() -> void:
	saveSettings()
	await get_tree().process_frame
	Game.edit()

func loadSettings() -> void:
	configFile.load("user://config.ini")
	%gameSettings.playGame = self
	%gameSettings.opened(configFile)

func saveSettings() -> void:
	%gameSettings.closed(configFile)
	configFile.save("user://config.ini")

func autoRun() -> void:
	Game.autoRun = !Game.autoRun
	AudioManager.play(preload("res://resources/sounds/autoRun.wav")).pitch_scale = 1.0 if Game.autoRun else 0.7
	autoRunTimer = 0
	saveSettings()
